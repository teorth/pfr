import Mathlib.Analysis.SpecialFunctions.Log.Deriv
import Mathlib.Analysis.SpecialFunctions.Pow.Asymptotics
import PFR.Mathlib.Analysis.Convex.Jensen

/-!
# Entropy function

The purpose of this file is to record basic analytic properties of the function
$$h(x) = - x * \log x$$ on the unit interval, for use in the theory of Shannon entropy.

## Main definitions

* `negMulLog`: the function `x ‚Ü¶ - x log x`

## Main results

* `sum_negMulLog_le`: a Jensen inequality for `negMulLog`
* `sum_negMulLog_eq`: the equality case of this inequality

-/

open scoped ENNReal NNReal Topology BigOperators

namespace Real

lemma tendsto_log_mul_nhds_zero_left :
    Filter.Tendsto (fun x ‚Ü¶ log x * x) (ùìù[<] 0) (ùìù 0) := by
  have h := tendsto_log_mul_rpow_nhds_zero zero_lt_one
  simp only [rpow_one] at h
  have h_eq : ‚àÄ x ‚àà Set.Iio 0, (- (fun x ‚Ü¶ log x * x) ‚àò (fun x ‚Ü¶ |x|)) x = log x * x := by
    intro x hx
    simp only [Set.mem_Iio] at hx
    simp only [Pi.neg_apply, Function.comp_apply, log_abs]
    rw [abs_of_nonpos hx.le]
    simp only [mul_neg, neg_neg]
  refine tendsto_nhdsWithin_congr h_eq ?_
  rw [‚Üê neg_zero]
  refine Filter.Tendsto.neg ?_
  simp only [neg_zero]
  refine h.comp ?_
  refine tendsto_abs_nhdsWithin_zero.mono_left ?_
  refine nhdsWithin_mono 0 (fun x hx ‚Ü¶ ?_)
  simp only [Set.mem_Iio] at hx
  simp only [Set.mem_compl_iff, Set.mem_singleton_iff, hx.ne, not_false_eq_true]

lemma continuous_mul_log : Continuous fun x ‚Ü¶ x * log x := by
  rw [continuous_iff_continuousAt]
  intro x
  by_cases hx : x = 0
  swap; ¬∑ exact (continuous_id'.continuousAt).mul (continuousAt_log hx)
  rw [hx]
  have h := tendsto_log_mul_rpow_nhds_zero zero_lt_one
  simp only [rpow_one] at h
  have h' : Filter.Tendsto (fun x ‚Ü¶ log x * x) (ùìù[<] 0) (ùìù 0) := tendsto_log_mul_nhds_zero_left
  rw [ContinuousAt, zero_mul]
  suffices Filter.Tendsto (fun x ‚Ü¶ log x * x) (ùìù 0) (ùìù 0) by
    exact this.congr (fun x ‚Ü¶ by rw [mul_comm])
  nth_rewrite 1 [‚Üê nhdsWithin_univ]
  have : (Set.univ : Set ‚Ñù) = Set.Iio 0 ‚à™ Set.Ioi 0 ‚à™ {0} := by
    ext x
    simp only [Set.mem_univ, Set.Iio_union_Ioi, Set.union_singleton, Set.mem_compl_iff,
      Set.mem_singleton_iff, not_true, Set.mem_insert_iff, true_iff]
    exact em _
  rw [this, nhdsWithin_union, nhdsWithin_union]
  simp only [ge_iff_le, nhdsWithin_singleton, sup_le_iff, Filter.nonpos_iff, Filter.tendsto_sup]
  refine ‚ü®‚ü®h', h‚ü©, ?_‚ü©
  rw [Filter.tendsto_pure_left, mul_zero]
  intro s hs
  obtain ‚ü®t, hts, _, h_zero_mem‚ü© := mem_nhds_iff.mp hs
  exact hts h_zero_mem

lemma differentiableOn_id_mul_log : DifferentiableOn ‚Ñù (fun x ‚Ü¶ x * log x) {0}·∂ú :=
  differentiable_id'.differentiableOn.mul differentiableOn_log

lemma deriv_id_mul_log {x : ‚Ñù} (hx : x ‚â† 0) : deriv (fun x ‚Ü¶ x * log x) x = log x + 1 := by
  rw [deriv_mul differentiableAt_id' (differentiableAt_log hx)]
  simp only [deriv_id'', one_mul, deriv_log', ne_eq, add_right_inj]
  exact mul_inv_cancel hx

lemma deriv2_id_mul_log {x : ‚Ñù} (hx : x ‚â† 0) : deriv^[2] (fun x ‚Ü¶ x * log x) x = x‚Åª¬π := by
  simp only [Function.iterate_succ, Function.iterate_zero, Function.comp.left_id,
    Function.comp_apply]
  suffices ‚àÄ·∂† y in (ùìù x), deriv (fun x ‚Ü¶ x * log x) y = log y + 1 by
    refine (Filter.EventuallyEq.deriv_eq this).trans ?_
    rw [deriv_add_const, deriv_log x]
  suffices ‚àÄ·∂† y in (ùìù x), y ‚â† 0 by
    filter_upwards [this] with y hy
    exact deriv_id_mul_log hy
  exact eventually_ne_nhds hx

lemma strictConvexOn_id_mul_log : StrictConvexOn ‚Ñù (Set.Ici (0 : ‚Ñù)) (fun x ‚Ü¶ x * log x) := by
  refine strictConvexOn_of_deriv2_pos (convex_Ici 0) (continuous_mul_log.continuousOn) ?_
  intro x hx
  simp only [Set.nonempty_Iio, interior_Ici', Set.mem_Ioi] at hx
  rw [deriv2_id_mul_log hx.ne']
  positivity

lemma convexOn_id_mul_log : ConvexOn ‚Ñù (Set.Ici (0 : ‚Ñù)) (fun x ‚Ü¶ x * log x) :=
  strictConvexOn_id_mul_log.convexOn

lemma id_mul_log_nonneg {x : ‚Ñù} (hx : 1 ‚â§ x) : 0 ‚â§ x * log x :=
  mul_nonneg (zero_le_one.trans hx) (log_nonneg hx)


section negMulLog

/-- The function `x ‚Ü¶ - x * log x` from `‚Ñù` to `‚Ñù`. -/
noncomputable
def negMulLog (x : ‚Ñù) : ‚Ñù := - x * log x

@[simp]
lemma negMulLog_zero : negMulLog (0 : ‚Ñù) = 0 := by simp [negMulLog]

@[simp]
lemma negMulLog_one : negMulLog (1 : ‚Ñù) = 0 := by simp [negMulLog]

lemma negMulLog_eq_neg : negMulLog = fun x ‚Ü¶ - (x * log x) := by
  funext
  simp [negMulLog]

lemma negMulLog_nonneg {x : ‚Ñù} (h1 : 0 ‚â§ x) (h2 : x ‚â§ 1) : 0 ‚â§ negMulLog x := by
  rw [negMulLog, neg_mul_comm]
  apply mul_nonneg h1
  simp only [Left.nonneg_neg_iff]
  exact log_nonpos h1 h2

lemma negMulLog_mul (x y : ‚Ñù) : negMulLog (x * y) = - (y * x * log x + x * y * log y) := by
  simp only [negMulLog, neg_mul, neg_add_rev]
  by_cases hx : x = 0
  ¬∑ simp [hx]
  by_cases hy : y = 0
  ¬∑ simp [hy]
  rw [log_mul hx hy]
  ring

lemma negMulLog_mul' (x y : ‚Ñù) : negMulLog (x * y) = y * negMulLog x + x * negMulLog y := by
  simp_rw [negMulLog_mul, negMulLog]
  ring

lemma concaveOn_negMulLog : ConcaveOn ‚Ñù (Set.Ici (0 : ‚Ñù)) negMulLog := by
  rw [negMulLog_eq_neg]
  exact convexOn_id_mul_log.neg

lemma strictConcaveOn_negMulLog : StrictConcaveOn ‚Ñù (Set.Ici (0 : ‚Ñù)) negMulLog := by
  rw [negMulLog_eq_neg]
  exact strictConvexOn_id_mul_log.neg

lemma sum_negMulLog_finset_le {S : Type*} {A : Finset S} {w : S ‚Üí ‚Ñù} {p : S ‚Üí ‚Ñù}
    (h0 : ‚àÄ s ‚àà A, 0 ‚â§ w s) (h1 : ‚àë s in A, w s = 1) (hmem : ‚àÄ s ‚àà A, 0 ‚â§ p s) :
    ‚àë s in A, w s * negMulLog (p s) ‚â§ negMulLog (‚àë s in A, w s * p s) :=
  ConcaveOn.le_map_sum concaveOn_negMulLog h0 h1 hmem

lemma sum_negMulLog_le {S : Type*} [Fintype S] {w : S ‚Üí ‚Ñù} {p : S ‚Üí ‚Ñù} (h0 : ‚àÄ s, 0 ‚â§ w s)
    (h1 : ‚àë s, w s = 1) (hmem : ‚àÄ s, 0 ‚â§ p s) :
    ‚àë s, w s * negMulLog (p s) ‚â§ negMulLog (‚àë s, w s * p s) :=
  sum_negMulLog_finset_le (fun s _hs ‚Ü¶ h0 s) h1 (fun s _hs ‚Ü¶ hmem s)

-- a form of equality case of Jensen
lemma sum_negMulLog_eq_aux {w : S ‚Üí ‚Ñù} {p : S ‚Üí ‚Ñù} {U : Finset S}
    (h0 : ‚àÄ s ‚àà U, 0 < w s) (h‚ÇÅ : ‚àë s in U, w s = 1) (hmem : ‚àÄ s ‚àà U, 0 ‚â§ p s) :
    negMulLog (‚àë s in U, w s ‚Ä¢ p s) = ‚àë s in U, w s ‚Ä¢ negMulLog (p s)
    ‚Üî ‚àÄ j ‚àà U, p j = ‚àë s in U, w s * p s :=
  strictConcaveOn_negMulLog.map_sum_eq_iff h0 h‚ÇÅ hmem

-- a form of equality case of Jensen
lemma sum_negMulLog_eq_aux2 {w : S ‚Üí ‚Ñù} {p : S ‚Üí ‚Ñù} {U : Finset S}
    (h0 : ‚àÄ s ‚àà U, 0 < w s) (h1 : ‚àë s in U, w s = 1) (hmem : ‚àÄ s ‚àà U, 0 ‚â§ p s) :
    ‚àë s in U, w s * negMulLog (p s) = negMulLog (‚àë s in U, w s * p s)
    ‚Üî ‚àÄ j ‚àà U, p j = ‚àë s in U, w s * p s := by
  rw [eq_comm]
  exact strictConcaveOn_negMulLog.map_sum_eq_iff h0 h1 hmem

-- a form of equality case of Jensen
lemma sum_negMulLog_eq_aux3 {S : Type*} [Fintype S] {w : S ‚Üí ‚Ñù} {p : S ‚Üí ‚Ñù} (h0 : ‚àÄ s, 0 ‚â§ w s)
    (h1 : ‚àë s, w s = 1) (hmem : ‚àÄ s, 0 ‚â§ p s) :
    ‚àë s, w s * negMulLog (p s) = negMulLog (‚àë s, w s * p s)
      ‚Üî ‚àÄ s, w s = 0 ‚à® p s = ‚àë s', w s' * p s' := by
  rw [eq_comm]
  exact strictConcaveOn_negMulLog.map_sum_eq_iff' h0 h1 hmem

/-- The equality case of Jensen's inequality -/
lemma sum_negMulLog_eq {S : Type*} [Fintype S] {w : S ‚Üí ‚Ñù} {p : S ‚Üí ‚Ñù} (h0 : ‚àÄ s, 0 ‚â§ w s)
    (h1 : ‚àë s, w s = 1) (hmem : ‚àÄ s, 0 ‚â§ p s)
    (heq : ‚àë s, w s * negMulLog (p s) = negMulLog (‚àë s, w s * p s))
    (s : S) (hs : 0 < w s) : p s = ‚àë s', (w s') * (p s') :=
  ((sum_negMulLog_eq_aux3 h0 h1 hmem).1 heq s).resolve_left hs.ne'

lemma continuous_negMulLog : Continuous negMulLog := by
  change Continuous fun x ‚Ü¶ - x * Real.log x
  simp_rw [neg_mul]
  exact continuous_mul_log.neg

end negMulLog
end Real
