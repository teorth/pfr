import PFR.ForMathlib.FiniteRange.Defs
import Mathlib.Probability.IdentDistrib
import PFR.ForMathlib.Entropy.Basic
import PFR.Mathlib.Analysis.SpecialFunctions.NegMulLog

/-!
# Kullback-Leibler divergence

Definition of Kullback-Leibler divergence and basic facts
-/

open MeasureTheory ProbabilityTheory Real

variable {Î© Î©' Î©'' Î©''' G: Type*}
  [mÎ© : MeasurableSpace Î©] {Î¼ : Measure Î©}
  [mÎ©' : MeasurableSpace Î©'] {Î¼' : Measure Î©'}
  [mÎ©'' : MeasurableSpace Î©''] {Î¼'' : Measure Î©''}
  [mÎ©''' : MeasurableSpace Î©'''] {Î¼''' : Measure Î©'''}
  [hG : MeasurableSpace G] -- [MeasurableSingletonClass G]

variable {X : Î© â†’ G} {Y : Î©' â†’ G} -- [FiniteRange X] [FiniteRange Y]

-- one should add somewhere the hypotheses that Î¼, Î¼', and Î¼'' are all probability measures

/-- If `X, Y` are two `G`-valued random variables, the Kullback--Leibler divergence is defined as
  `KL(X â€– Y) := âˆ‘â‚“ ð(X = x) log (ð(X = x) / ð(Y = x))`.

Note that due to Lean conventions with junk values, this definition only makes sense when
`X` is absolutely continuous wrt to `Y`, i.e., `âˆ€ x, ð(Y = x) = 0 â†’ ð(X = x) = 0`. Otherwise, the
divergence should be infinite, but since we use real numbers for ease of computations, this is
not a possible choice.
  -/
noncomputable def KL_div (X : Î© â†’ G) (Y: Î©' â†’ G) (Î¼ : Measure Î© := by volume_tac)
    (Î¼' : Measure Î©' := by volume_tac) : â„ :=
  âˆ‘' x, (Î¼.map X {x}).toReal * log ((Î¼.map X {x}).toReal / (Î¼'.map Y {x}).toReal)

@[inherit_doc KL_div] notation3:max "KL[" X " ; " Î¼ " # " Y " ; " Î¼' "]" => KL_div X Y Î¼ Î¼'

@[inherit_doc KL_div] notation3:max "KL[" X " # " Y "]" => KL_div X Y volume volume

/-- If `X'` is a copy of `X`, and `Y'` is a copy of `Y`, then `KL(X' â€– Y') = KL(X â€– Y)`. -/
lemma KL_div_eq_of_equiv (X' : Î©'' â†’ G) (Y' : Î©''' â†’ G)
    (hX : IdentDistrib X X' Î¼ Î¼'') (hY : IdentDistrib Y Y' Î¼' Î¼''') :
    KL[X ; Î¼ # Y ; Î¼'] = KL[X' ; Î¼'' # Y' ; Î¼'''] := by
  simp only [KL_div]
  congr with x
  congr
  Â· exact hX.map_eq
  Â· exact hX.map_eq
  Â· exact hY.map_eq

lemma KL_div_eq_sum [Fintype G] :
    KL[X ; Î¼ # Y ; Î¼'] = âˆ‘ x,
      (Î¼.map X {x}).toReal * log ((Î¼.map X {x}).toReal / (Î¼'.map Y {x}).toReal) :=
  tsum_eq_sum (by simp)

/-- `KL(X â€– Y) â‰¥ 0`.-/
lemma KL_div_nonneg [Fintype G] [MeasurableSingletonClass G] [IsProbabilityMeasure Î¼]
    [IsProbabilityMeasure Î¼'] (hX : Measurable X) (hY : Measurable Y)
    (habs : âˆ€ x, Î¼'.map Y {x} = 0 â†’ Î¼.map X {x} = 0) : 0 â‰¤ KL[X ; Î¼ # Y ; Î¼'] := by
  rw [KL_div_eq_sum]
  apply le_trans ?_ (sum_mul_log_div_leq (by simp) (by simp) ?_)
  Â· have : IsProbabilityMeasure (Î¼'.map Y) := isProbabilityMeasure_map hY.aemeasurable
    have : IsProbabilityMeasure (Î¼.map X) := isProbabilityMeasure_map hX.aemeasurable
    simp
  Â· intro i _ hi
    simp only [ENNReal.toReal_eq_zero_iff, measure_ne_top, or_false] at hi
    simp [habs i hi]

/-- `KL(X â€– Y) = 0` if and only if `Y` is a copy of `X`. -/
lemma KL_div_eq_zero_iff_identDistrib [Fintype G] [MeasurableSingletonClass G]
    [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î¼'] (hX : Measurable X) (hY : Measurable Y)
    (habs : âˆ€ x, Î¼'.map Y {x} = 0 â†’ Î¼.map X {x} = 0) :
    KL[X ; Î¼ # Y ; Î¼'] = 0 â†” IdentDistrib X Y Î¼ Î¼' := by
  have L (x : â„) : log (x / x) = 0 := by
    rcases eq_or_ne x 0 with rfl | hx
    Â· simp
    Â· simp [hx]
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ by simp [KL_div, h.map_eq, L]âŸ©
  let Î½Y := Î¼'.map Y
  have : IsProbabilityMeasure Î½Y := isProbabilityMeasure_map hY.aemeasurable
  let Î½X := Î¼.map X
  have : IsProbabilityMeasure Î½X := isProbabilityMeasure_map hX.aemeasurable
  obtain âŸ¨r, hrâŸ© : âˆƒ (r : â„), âˆ€ x âˆˆ Finset.univ, (Î½X {x}).toReal = r * (Î½Y {x}).toReal := by
    apply sum_mul_log_div_eq_iff (by simp) (by simp) (fun i _ hi â†¦ ?_)
    Â· rw [KL_div_eq_sum] at h
      simpa using h
    Â· simp only [ENNReal.toReal_eq_zero_iff, measure_ne_top, or_false] at hi
      simp [habs i hi]
  have r_eq : r = 1 := by
    have : r * âˆ‘ x, (Î½Y {x}).toReal = âˆ‘ x, (Î½X {x}).toReal := by
      simp only [Finset.mul_sum, Finset.mem_univ, hr]
    simpa using this
  have : Î½X = Î½Y := by
    apply Measure.ext_iff_singleton.mpr (fun x â†¦ ?_)
    simpa [r_eq, ENNReal.toReal_eq_toReal] using hr x (Finset.mem_univ _)
  exact âŸ¨hX.aemeasurable, hY.aemeasurable, thisâŸ©

/-- If $S$ is a finite set, $w_s$ is non-negative,
and ${\bf P}(X=x) = \sum_{s\in S} w_s {\bf P}(X_s=x)$, ${\bf P}(Y=x) =
  \sum_{s\in S} w_s {\bf P}(Y_s=x)$ for all $x$, then
$$D_{KL}(X\Vert Y) \le \sum_{s\in S} w_s D_{KL}(X_s\Vert Y_s).$$ -/
lemma KL_div_of_convex [Fintype G] [MeasurableSingletonClass G] [IsFiniteMeasure Î¼''']
    {Î¹ : Type*} {S : Finset Î¹} {w : Î¹ â†’ â„} (hw : âˆ€ s âˆˆ S, 0 â‰¤ w s)
    (X' : Î¹ â†’ Î©'' â†’ G) (Y' : Î¹ â†’ Î©''' â†’ G)
    (hconvex : âˆ€ x, (Î¼.map X {x}).toReal = âˆ‘ s âˆˆ S, (w s) * (Î¼''.map (X' s) {x}).toReal)
    (hconvex' : âˆ€ x, (Î¼'.map Y {x}).toReal = âˆ‘ s âˆˆ S, (w s) * (Î¼'''.map (Y' s) {x}).toReal)
    (habs : âˆ€ s âˆˆ S, âˆ€ x, Î¼'''.map (Y' s) {x} = 0 â†’ Î¼''.map (X' s) {x} = 0) :
    KL[X ; Î¼ # Y ; Î¼'] â‰¤ âˆ‘ s âˆˆ S, w s * KL[X' s ; Î¼'' # Y' s ; Î¼'''] := by
  conv_lhs => rw [KL_div_eq_sum]
  have A x : (Î¼.map X {x}).toReal * log ((Î¼.map X {x}).toReal / (Î¼'.map Y {x}).toReal)
    â‰¤ âˆ‘ s âˆˆ S, (w s * (Î¼''.map (X' s) {x}).toReal) *
        log ((w s * (Î¼''.map (X' s) {x}).toReal) / (w s * (Î¼'''.map (Y' s) {x}).toReal)) := by
    rw [hconvex, hconvex']
    apply sum_mul_log_div_leq (fun s hs â†¦ ?_) (fun s hs â†¦ ?_) (fun s hs h's â†¦ ?_)
    Â· exact mul_nonneg (by simp [hw s hs]) (by simp)
    Â· exact mul_nonneg (by simp [hw s hs]) (by simp)
    Â· rcases mul_eq_zero.1 h's with h | h
      Â· simp [h]
      Â· simp only [ENNReal.toReal_eq_zero_iff, measure_ne_top, or_false] at h
        simp [habs s hs x h]
  have B x : (Î¼.map X {x}).toReal * log ((Î¼.map X {x}).toReal / (Î¼'.map Y {x}).toReal)
    â‰¤ âˆ‘ s âˆˆ S, (w s * (Î¼''.map (X' s) {x}).toReal) *
        log ((Î¼''.map (X' s) {x}).toReal / (Î¼'''.map (Y' s) {x}).toReal) := by
    apply (A x).trans_eq
    apply Finset.sum_congr rfl (fun s _ â†¦ ?_)
    rcases eq_or_ne (w s) 0 with h's | h's
    Â· simp [h's]
    Â· congr 2
      rw [mul_div_mul_left _ _ h's]
  apply (Finset.sum_le_sum (fun x _ â†¦ B x)).trans_eq
  rw [Finset.sum_comm]
  simp_rw [mul_assoc, â† Finset.mul_sum, KL_div_eq_sum]

/-- If $f:G \to H$ is an injection, then $D_{KL}(f(X)\Vert f(Y)) = D_{KL}(X\Vert Y)$. -/
lemma KL_div_of_comp_inj {H : Type*} [MeasurableSpace H] [DiscreteMeasurableSpace G]
    [MeasurableSingletonClass H] {f : G â†’ H}
    (hf : Function.Injective f) (hX : Measurable X) (hY : Measurable Y) :
    KL[X ; Î¼ # Y ; Î¼'] = KL[f âˆ˜ X ; Î¼ # f âˆ˜ Y ; Î¼'] := by
  simp [KL_div]
  rw [â† hf.tsum_eq]
  Â· congr with x
    have : (Measure.map X Î¼) {x} = (Measure.map (f âˆ˜ X) Î¼) {f x} := by
      rw [Measure.map_apply, Measure.map_apply]
      Â· congr
        exact Set.Subset.antisymm (fun â¦ƒaâ¦„ â†¦ congrArg f) fun â¦ƒaâ¦„ a_1 â†¦ hf a_1
      Â· exact Measurable.of_discrete.comp hX
      Â· exact measurableSet_singleton (f x)
      Â· exact hX
      Â· exact measurableSet_singleton x
    have :  (Measure.map Y Î¼') {x} = (Measure.map (f âˆ˜ Y) Î¼') {f x} := by
      rw [Measure.map_apply, Measure.map_apply]
      Â· congr
        exact Set.Subset.antisymm (fun â¦ƒaâ¦„ â†¦ congrArg f) fun â¦ƒaâ¦„ a_1 â†¦ hf a_1
      Â· exact Measurable.of_discrete.comp hY
      Â· exact measurableSet_singleton (f x)
      Â· exact hY
      Â· exact measurableSet_singleton x
    congr
  Â· intro y hy
    have : Measure.map (f âˆ˜ X) Î¼ {y} â‰  0 := by
      intro h
      simp [h] at hy
    rw [Measure.map_apply (Measurable.of_discrete.comp hX) (measurableSet_singleton y)] at this
    have : f âˆ˜ X â»Â¹' {y} â‰  âˆ… := by
      intro h
      simp [h] at this
    obtain âŸ¨z, hzâŸ© := Set.nonempty_iff_ne_empty.2 this
    simp only [Set.mem_preimage, Function.comp_apply, Set.mem_singleton_iff] at hz
    exact Set.mem_range.2 âŸ¨X z, hzâŸ©

/-- If $X, Y, Z$ are independent $G$-valued random variables, then
  $$D_{KL}(X+Z\Vert Y+Z) \leq D_{KL}(X\Vert Y).$$ -/
lemma KL_div_add_le_KL_div_of_indep [AddCommGroup G] [MeasurableSubâ‚‚ G] [MeasurableAddâ‚‚ G]
    {X Y Z : Î© â†’ G} [FiniteRange X] [FiniteRange Y] [FiniteRange Z]
    (hindep : iIndepFun (fun _ â†¦ hG) ![X, Y, Z] Î¼) :
    KL[X + Z ; Î¼ # Y + Z ; Î¼] â‰¤ KL[X ; Î¼ # Y ; Î¼] := sorry

/-- If $X,Y,Z$ are random variables, with $X,Z$ defined on the same sample space, we define
$$ D_{KL}(X|Z \Vert Y) := \sum_z \mathbf{P}(Z=z) D_{KL}( (X|Z=z) \Vert Y).$$ -/
noncomputable def condKL_div {S: Type*} (X: Î© â†’ G) (Y: Î©' â†’ G) (Z: Î© â†’ S)
    (Î¼ : Measure Î© := by volume_tac) (Î¼' : Measure Î©' := by volume_tac) : â„ :=
  âˆ‘' z, (Î¼ (Zâ»Â¹' {z})).toReal * KL[X ; (ProbabilityTheory.cond Î¼ (Zâ»Â¹' {z})) # Y ; Î¼']


@[inherit_doc condKL_div] notation3:max "KL[" X " | " Z " ; " Î¼ " # " Y " ; " Î¼' "]" => condKL_div X Y Z Î¼ Î¼'

@[inherit_doc condKL_div] notation3:max "KL[" X " | " Z " # " Y "]" => condKL_div X Y Z volume volume

/-- If $X, Y$ are independent $G$-valued random variables, and $Z$ is another random variable defined on the same sample space as $X$, then
  $$D_{KL}((X|Z)\Vert Y) = D_{KL}(X\Vert Y) + \bbH[X] - \bbH[X|Z].$$ -/
lemma condKL_div_eq {S: Type*} [MeasurableSpace S] (X : Î© â†’ G) (Y : Î©' â†’ G) (Z : Î© â†’ S)
    (Î¼ : Measure Î© := by volume_tac) (Î¼' : Measure Î©' := by volume_tac) :
    KL[ X | Z; Î¼ # Y ; Î¼'] = KL[X ; Î¼ # Y ; Î¼'] + H[X ; Î¼] - H[ X | Z ; Î¼] := sorry

/-- `KL(X|Z â€– Y) â‰¥ 0`.-/
lemma condKL_div_nonneg {S : Type*} (X : Î© â†’ G) (Y : Î©' â†’ G) (Z : Î© â†’ S)
    (Î¼ : Measure Î© := by volume_tac) (Î¼' : Measure Î©' := by volume_tac) :
    0 â‰¤ KL[X | Z; Î¼ # Y ; Î¼'] := sorry
