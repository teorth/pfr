import PFR.ForMathlib.FiniteRange.Defs
import Mathlib.Probability.IdentDistrib
import PFR.ForMathlib.Entropy.Basic
import PFR.Mathlib.Analysis.SpecialFunctions.NegMulLog
import PFR.Mathlib.Analysis.SpecialFunctions.Log.Basic

/-!
# Kullback-Leibler divergence

Definition of Kullback-Leibler divergence and basic facts
-/

open MeasureTheory ProbabilityTheory Real

variable {Î© Î©' Î©'' Î©''' G: Type*}
  [mÎ© : MeasurableSpace Î©] {Î¼ : Measure Î©}
  [mÎ©' : MeasurableSpace Î©'] {Î¼' : Measure Î©'}
  [mÎ©'' : MeasurableSpace Î©''] {Î¼'' : Measure Î©''}
  [mÎ©''' : MeasurableSpace Î©'''] {Î¼''' : Measure Î©'''}
  [hG : MeasurableSpace G]

variable {X : Î© â†’ G} {Y : Î©' â†’ G}

/-- If `X, Y` are two `G`-valued random variables, the Kullback--Leibler divergence is defined as
  `KL(X â€– Y) := âˆ‘â‚“ ğ(X = x) log (ğ(X = x) / ğ(Y = x))`.

Note that this definition only makes sense when `X` is absolutely continuous wrt to `Y`,
i.e., `âˆ€ x, ğ(Y = x) = 0 â†’ ğ(X = x) = 0`. Otherwise, the divergence should be infinite, but since
we use real numbers for ease of computations, this is not a possible choice.
  -/
noncomputable def KLDiv (X : Î© â†’ G) (Y : Î©' â†’ G) (Î¼ : Measure Î© := by volume_tac)
    (Î¼' : Measure Î©' := by volume_tac) : â„ :=
  âˆ‘' x, (Î¼.map X {x}).toReal * log ((Î¼.map X {x}).toReal / (Î¼'.map Y {x}).toReal)

@[inherit_doc KLDiv] notation3:max "KL[" X " ; " Î¼ " # " Y " ; " Î¼' "]" => KLDiv X Y Î¼ Î¼'

@[inherit_doc KLDiv] notation3:max "KL[" X " # " Y "]" => KLDiv X Y volume volume

@[simp] lemma KLDiv_self : KL[X ; Î¼ # X ; Î¼] = 0 := by simp [KLDiv]

/-- If `X'` is a copy of `X`, and `Y'` is a copy of `Y`, then `KL(X' â€– Y') = KL(X â€– Y)`. -/
lemma KLDiv_eq_of_equiv (X' : Î©'' â†’ G) (Y' : Î©''' â†’ G)
    (hX : IdentDistrib X X' Î¼ Î¼'') (hY : IdentDistrib Y Y' Î¼' Î¼''') :
    KL[X ; Î¼ # Y ; Î¼'] = KL[X' ; Î¼'' # Y' ; Î¼'''] := by
  simp only [KLDiv]
  congr with x
  congr
  Â· exact hX.map_eq
  Â· exact hX.map_eq
  Â· exact hY.map_eq

lemma KLDiv_eq_sum [Fintype G] :
    KL[X ; Î¼ # Y ; Î¼'] = âˆ‘ x,
      (Î¼.map X {x}).toReal * log ((Î¼.map X {x}).toReal / (Î¼'.map Y {x}).toReal) :=
  tsum_eq_sum (by simp)

/-- `KL(X â€– Y) â‰¥ 0`.-/
lemma KLDiv_nonneg [Fintype G] [MeasurableSingletonClass G] [IsZeroOrProbabilityMeasure Î¼]
    [IsZeroOrProbabilityMeasure Î¼'] (hX : Measurable X) (hY : Measurable Y)
    (habs : âˆ€ x, Î¼'.map Y {x} = 0 â†’ Î¼.map X {x} = 0) : 0 â‰¤ KL[X ; Î¼ # Y ; Î¼'] := by
  rw [KLDiv_eq_sum]
  rcases eq_zero_or_isProbabilityMeasure Î¼ with rfl | hÎ¼
  Â· simp
  rcases eq_zero_or_isProbabilityMeasure Î¼' with rfl | hÎ¼'
  Â· simp
  apply le_trans ?_ (sum_mul_log_div_leq (by simp) (by simp) ?_)
  Â· have : IsProbabilityMeasure (Î¼'.map Y) := isProbabilityMeasure_map hY.aemeasurable
    have : IsProbabilityMeasure (Î¼.map X) := isProbabilityMeasure_map hX.aemeasurable
    simp
  Â· intro i _ hi
    simp only [ENNReal.toReal_eq_zero_iff, measure_ne_top, or_false] at hi
    simp [habs i hi]


/-- `KL(X â€– Y) = 0` if and only if `Y` is a copy of `X`. -/
lemma KLDiv_eq_zero_iff_identDistrib [Fintype G] [MeasurableSingletonClass G]
    [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î¼'] (hX : Measurable X) (hY : Measurable Y)
    (habs : âˆ€ x, Î¼'.map Y {x} = 0 â†’ Î¼.map X {x} = 0) :
    KL[X ; Î¼ # Y ; Î¼'] = 0 â†” IdentDistrib X Y Î¼ Î¼' := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ by simp [KLDiv, h.map_eq]âŸ©
  let Î½Y := Î¼'.map Y
  have : IsProbabilityMeasure Î½Y := isProbabilityMeasure_map hY.aemeasurable
  let Î½X := Î¼.map X
  have : IsProbabilityMeasure Î½X := isProbabilityMeasure_map hX.aemeasurable
  obtain âŸ¨r, hrâŸ© : âˆƒ (r : â„), âˆ€ x âˆˆ Finset.univ, (Î½X {x}).toReal = r * (Î½Y {x}).toReal := by
    apply sum_mul_log_div_eq_iff (by simp) (by simp) (fun i _ hi â†¦ ?_)
    Â· rw [KLDiv_eq_sum] at h
      simpa using h
    Â· simp only [ENNReal.toReal_eq_zero_iff, measure_ne_top, or_false] at hi
      simp [habs i hi]
  have r_eq : r = 1 := by
    have : r * âˆ‘ x, (Î½Y {x}).toReal = âˆ‘ x, (Î½X {x}).toReal := by
      simp only [Finset.mul_sum, Finset.mem_univ, hr]
    simpa using this
  have : Î½X = Î½Y := by
    apply Measure.ext_iff_singleton.mpr (fun x â†¦ ?_)
    simpa [r_eq, ENNReal.toReal_eq_toReal] using hr x (Finset.mem_univ _)
  exact âŸ¨hX.aemeasurable, hY.aemeasurable, thisâŸ©

/-- If $S$ is a finite set, $w_s$ is non-negative,
and ${\bf P}(X=x) = \sum_{s\in S} w_s {\bf P}(X_s=x)$, ${\bf P}(Y=x) =
  \sum_{s\in S} w_s {\bf P}(Y_s=x)$ for all $x$, then
$$D_{KL}(X\Vert Y) \le \sum_{s\in S} w_s D_{KL}(X_s\Vert Y_s).$$ -/
lemma KLDiv_of_convex [Fintype G] [IsFiniteMeasure Î¼''']
    {Î¹ : Type*} {S : Finset Î¹} {w : Î¹ â†’ â„} (hw : âˆ€ s âˆˆ S, 0 â‰¤ w s)
    (X' : Î¹ â†’ Î©'' â†’ G) (Y' : Î¹ â†’ Î©''' â†’ G)
    (hconvex : âˆ€ x, (Î¼.map X {x}).toReal = âˆ‘ s âˆˆ S, (w s) * (Î¼''.map (X' s) {x}).toReal)
    (hconvex' : âˆ€ x, (Î¼'.map Y {x}).toReal = âˆ‘ s âˆˆ S, (w s) * (Î¼'''.map (Y' s) {x}).toReal)
    (habs : âˆ€ s âˆˆ S, âˆ€ x, Î¼'''.map (Y' s) {x} = 0 â†’ Î¼''.map (X' s) {x} = 0) :
    KL[X ; Î¼ # Y ; Î¼'] â‰¤ âˆ‘ s âˆˆ S, w s * KL[X' s ; Î¼'' # Y' s ; Î¼'''] := by
  conv_lhs => rw [KLDiv_eq_sum]
  have A x : (Î¼.map X {x}).toReal * log ((Î¼.map X {x}).toReal / (Î¼'.map Y {x}).toReal)
    â‰¤ âˆ‘ s âˆˆ S, (w s * (Î¼''.map (X' s) {x}).toReal) *
        log ((w s * (Î¼''.map (X' s) {x}).toReal) / (w s * (Î¼'''.map (Y' s) {x}).toReal)) := by
    rw [hconvex, hconvex']
    apply sum_mul_log_div_leq (fun s hs â†¦ ?_) (fun s hs â†¦ ?_) (fun s hs h's â†¦ ?_)
    Â· exact mul_nonneg (by simp [hw s hs]) (by simp)
    Â· exact mul_nonneg (by simp [hw s hs]) (by simp)
    Â· rcases mul_eq_zero.1 h's with h | h
      Â· simp [h]
      Â· simp only [ENNReal.toReal_eq_zero_iff, measure_ne_top, or_false] at h
        simp [habs s hs x h]
  have B x : (Î¼.map X {x}).toReal * log ((Î¼.map X {x}).toReal / (Î¼'.map Y {x}).toReal)
    â‰¤ âˆ‘ s âˆˆ S, (w s * (Î¼''.map (X' s) {x}).toReal) *
        log ((Î¼''.map (X' s) {x}).toReal / (Î¼'''.map (Y' s) {x}).toReal) := by
    apply (A x).trans_eq
    apply Finset.sum_congr rfl (fun s _ â†¦ ?_)
    rcases eq_or_ne (w s) 0 with h's | h's
    Â· simp [h's]
    Â· congr 2
      rw [mul_div_mul_left _ _ h's]
  apply (Finset.sum_le_sum (fun x _ â†¦ B x)).trans_eq
  rw [Finset.sum_comm]
  simp_rw [mul_assoc, â† Finset.mul_sum, KLDiv_eq_sum]

/-- If $f:G \to H$ is an injection, then $D_{KL}(f(X)\Vert f(Y)) = D_{KL}(X\Vert Y)$. -/
lemma KLDiv_of_comp_inj {H : Type*} [MeasurableSpace H] [DiscreteMeasurableSpace G]
    [MeasurableSingletonClass H] {f : G â†’ H}
    (hf : Function.Injective f) (hX : Measurable X) (hY : Measurable Y) :
    KL[f âˆ˜ X ; Î¼ # f âˆ˜ Y ; Î¼'] = KL[X ; Î¼ # Y ; Î¼'] := by
  simp only [KLDiv]
  rw [â† hf.tsum_eq]
  Â· symm
    congr with x
    have : (Measure.map X Î¼) {x} = (Measure.map (f âˆ˜ X) Î¼) {f x} := by
      rw [Measure.map_apply, Measure.map_apply]
      Â· congr
        exact Set.Subset.antisymm (fun â¦ƒaâ¦„ â†¦ congrArg f) fun â¦ƒaâ¦„ a_1 â†¦ hf a_1
      Â· exact Measurable.of_discrete.comp hX
      Â· exact measurableSet_singleton (f x)
      Â· exact hX
      Â· exact measurableSet_singleton x
    have :  (Measure.map Y Î¼') {x} = (Measure.map (f âˆ˜ Y) Î¼') {f x} := by
      rw [Measure.map_apply, Measure.map_apply]
      Â· congr
        exact Set.Subset.antisymm (fun â¦ƒaâ¦„ â†¦ congrArg f) fun â¦ƒaâ¦„ a_1 â†¦ hf a_1
      Â· exact Measurable.of_discrete.comp hY
      Â· exact measurableSet_singleton (f x)
      Â· exact hY
      Â· exact measurableSet_singleton x
    congr
  Â· intro y hy
    have : Measure.map (f âˆ˜ X) Î¼ {y} â‰  0 := by
      intro h
      simp [h] at hy
    rw [Measure.map_apply (Measurable.of_discrete.comp hX) (measurableSet_singleton y)] at this
    have : f âˆ˜ X â»Â¹' {y} â‰  âˆ… := by
      intro h
      simp [h] at this
    obtain âŸ¨z, hzâŸ© := Set.nonempty_iff_ne_empty.2 this
    simp only [Set.mem_preimage, Function.comp_apply, Set.mem_singleton_iff] at hz
    exact Set.mem_range.2 âŸ¨X z, hzâŸ©

open Set

open scoped Pointwise

/-- The distribution of `X + Z` is the convolution of the distributions of `Z` and `X` when these
random variables are independent.
Probably already available somewhere in some form, but I couldn't locate it. -/
lemma ProbabilityTheory.IndepFun.map_add_eq_sum
    [Fintype G] [AddCommGroup G] [DiscreteMeasurableSpace G]
    {X Z : Î© â†’ G} (hindep : IndepFun X Z Î¼)
    (hX : Measurable X) (hZ : Measurable Z) (S : Set G) :
    Î¼.map (X + Z) S = âˆ‘ s, Î¼.map Z {s} * Î¼.map X ({-s} + S) := by
  rw [Measure.map_apply (by fun_prop) (DiscreteMeasurableSpace.forall_measurableSet S)]
  have : (X + Z) â»Â¹' S = â‹ƒ s, X â»Â¹' ({-s} + S) âˆ© Z â»Â¹' {s} := by
    apply Subset.antisymm
    Â· intro y hy
      simp only [mem_iUnion, mem_inter_iff, mem_preimage, mem_singleton_iff, exists_and_left,
        exists_prop]
      simp at hy
      exact âŸ¨Z y, by simpa [add_comm] using hy, rflâŸ©
    Â· simp only [iUnion_subset_iff]
      intro i y hy
      simp only [singleton_add, image_add_left, neg_neg, mem_inter_iff, mem_preimage,
        mem_singleton_iff] at hy
      simp [hy.1, hy.2, add_comm]
  rw [this, measure_iUnion, tsum_fintype]; rotate_left
  Â· intro i j hij
    simp [Function.onFun]
    apply Disjoint.inter_left'
    apply Disjoint.inter_right'
    apply disjoint_left.2 (fun a ha hb â†¦ ?_)
    simp [â† neg_eq_iff_eq_neg] at ha hb
    rw [â† ha, â† hb] at hij
    exact hij rfl
  Â· intro i
    exact (hX (DiscreteMeasurableSpace.forall_measurableSet _)).inter (hZ (measurableSet_singleton _))
  congr with i
  rw [hindep.measure_inter_preimage_eq_mul _ _ (DiscreteMeasurableSpace.forall_measurableSet _)
    (measurableSet_singleton _), mul_comm,
    Measure.map_apply hZ (measurableSet_singleton _),
    Measure.map_apply hX (DiscreteMeasurableSpace.forall_measurableSet _)]

/-- The distribution of `X + Z` is the convolution of the distributions of `Z` and `X` when these
random variables are independent.
Probably already available somewhere in some form, but I couldn't locate it. -/
lemma ProbabilityTheory.IndepFun.map_add_singleton_eq_sum
    [Fintype G] [AddCommGroup G] [DiscreteMeasurableSpace G]
    {X Z : Î© â†’ G} (hindep : IndepFun X Z Î¼)
    (hX : Measurable X) (hZ : Measurable Z) (x : G) :
    Î¼.map (X + Z) {x} = âˆ‘ s, Î¼.map Z {s} * Î¼.map X {x - s} := by
  rw [hindep.map_add_eq_sum hX hZ]
  congr with s
  congr
  simp
  abel

/-- If $X, Y, Z$ are independent $G$-valued random variables, then
  $$D_{KL}(X+Z\Vert Y+Z) \leq D_{KL}(X\Vert Y).$$ -/
lemma KLDiv_add_le_KLDiv_of_indep [Fintype G] [AddCommGroup G] [DiscreteMeasurableSpace G]
    {X Y Z : Î© â†’ G} [IsZeroOrProbabilityMeasure Î¼]
    (hindep : IndepFun (âŸ¨X, YâŸ©) Z Î¼)
    (hX : Measurable X) (hY : Measurable Y) (hZ : Measurable Z)
    (habs : âˆ€ x, Î¼.map Y {x} = 0 â†’ Î¼.map X {x} = 0) :
    KL[X + Z ; Î¼ # Y + Z ; Î¼] â‰¤ KL[X ; Î¼ # Y ; Î¼] := by
  rcases eq_zero_or_isProbabilityMeasure Î¼ with rfl | hÎ¼
  Â· simp [KLDiv]
  set X' : G â†’ Î© â†’ G := fun s â†¦ (Â· + s) âˆ˜ X with hX'
  set Y' : G â†’ Î© â†’ G := fun s â†¦ (Â· + s) âˆ˜ Y with hY'
  have AX' x i : Î¼.map (X' i) {x} = Î¼.map X {x - i} := by
    rw [hX', â† Measure.map_map (by fun_prop) (by fun_prop),
      Measure.map_apply (by fun_prop) (measurableSet_singleton x)]
    congr
    ext y
    simp [sub_eq_add_neg]
  have AY' x i : Î¼.map (Y' i) {x} = Î¼.map Y {x - i} := by
    rw [hY', â† Measure.map_map (by fun_prop) (by fun_prop),
      Measure.map_apply (by fun_prop) (measurableSet_singleton x)]
    congr
    ext y
    simp [sub_eq_add_neg]
  let w : G â†’ â„ := fun s â†¦ (Î¼.map Z {s}).toReal
  let S : Finset G := Finset.univ
  have sum_w : âˆ‘ s, w s = 1 := by
    have : IsProbabilityMeasure (Î¼.map Z) := isProbabilityMeasure_map hZ.aemeasurable
    simp [w]
  have A x : (Î¼.map (X + Z) {x}).toReal = âˆ‘ s âˆˆ S, w s * (Î¼.map (X' s) {x}).toReal := by
    have : IndepFun X Z Î¼ := hindep.comp (Ï† := Prod.fst) (Ïˆ := id) measurable_fst measurable_id
    rw [this.map_add_singleton_eq_sum hX hZ, ENNReal.toReal_sum (by simp [ENNReal.mul_eq_top])]
    simp only [ENNReal.toReal_mul]
    congr with i
    congr 1
    rw [AX']
  have B x : (Î¼.map (Y + Z) {x}).toReal = âˆ‘ s âˆˆ S, w s * (Î¼.map (Y' s) {x}).toReal := by
    have : IndepFun Y Z Î¼ := hindep.comp (Ï† := Prod.snd) (Ïˆ := id) measurable_snd measurable_id
    rw [this.map_add_singleton_eq_sum hY hZ, ENNReal.toReal_sum (by simp [ENNReal.mul_eq_top])]
    simp only [ENNReal.toReal_mul]
    congr with i
    congr 1
    rw [AY']
  have : KL[X + Z ; Î¼ # Y + Z; Î¼] â‰¤ âˆ‘ s âˆˆ S, w s * KL[X' s ; Î¼ # Y' s ; Î¼] := by
    apply KLDiv_of_convex (fun s _ â†¦ by simp [w])
    Â· exact A
    Â· exact B
    Â· intro s _ x
      rw [AX', AY']
      exact habs _
  apply this.trans_eq
  have C s : KL[X' s ; Î¼ # Y' s ; Î¼] = KL[X ; Î¼ # Y ; Î¼] :=
    KLDiv_of_comp_inj (add_left_injective s) hX hY
  simp_rw [C, â† Finset.sum_mul, sum_w, one_mul]

/-- If $X,Y,Z$ are random variables, with $X,Z$ defined on the same sample space, we define
$$ D_{KL}(X|Z \Vert Y) := \sum_z \mathbf{P}(Z=z) D_{KL}( (X|Z=z) \Vert Y).$$ -/
noncomputable def condKLDiv {S: Type*} (X : Î© â†’ G) (Y : Î©' â†’ G) (Z : Î© â†’ S)
    (Î¼ : Measure Î© := by volume_tac) (Î¼' : Measure Î©' := by volume_tac) : â„ :=
  âˆ‘' z, (Î¼ (Zâ»Â¹' {z})).toReal * KL[X ; (ProbabilityTheory.cond Î¼ (Zâ»Â¹' {z})) # Y ; Î¼']

@[inherit_doc condKLDiv]
notation3:max "KL[" X " | " Z " ; " Î¼ " # " Y " ; " Î¼' "]" => condKLDiv X Y Z Î¼ Î¼'

@[inherit_doc condKLDiv]
notation3:max "KL[" X " | " Z " # " Y "]" => condKLDiv X Y Z volume volume

/-- If $X, Y$ are $G$-valued random variables, and $Z$ is another random variable
  defined on the same sample space as $X$, then
  $$D_{KL}((X|Z)\Vert Y) = D_{KL}(X\Vert Y) + \bbH[X] - \bbH[X|Z].$$ -/
lemma condKLDiv_eq {S : Type*} [MeasurableSpace S] [Fintype S] [MeasurableSingletonClass S]
    [Fintype G] [IsZeroOrProbabilityMeasure Î¼] [IsFiniteMeasure Î¼']
    {X : Î© â†’ G} {Y : Î©' â†’ G} {Z : Î© â†’ S}
    (hX : Measurable X) (hZ : Measurable Z)
    (habs : âˆ€ x, Î¼'.map Y {x} = 0 â†’ Î¼.map X {x} = 0) :
    KL[ X | Z ; Î¼ # Y ; Î¼'] = KL[X ; Î¼ # Y ; Î¼'] + H[X ; Î¼] - H[ X | Z ; Î¼] := by
  rcases eq_zero_or_isProbabilityMeasure Î¼ with rfl | hÎ¼
  Â· simp [condKLDiv, tsum_fintype, KLDiv_eq_sum, Finset.mul_sum, entropy_eq_sum]
  simp only [condKLDiv, tsum_fintype, KLDiv_eq_sum, Finset.mul_sum, entropy_eq_sum]
  rw [Finset.sum_comm, condEntropy_eq_sum_sum_fintype hZ, Finset.sum_comm (Î± := G),
    â† Finset.sum_add_distrib, â† Finset.sum_sub_distrib]
  congr with g
  simp only [negMulLog, neg_mul, Finset.sum_neg_distrib, mul_neg, sub_neg_eq_add, â† sub_eq_add_neg,
    â† mul_sub]
  simp_rw [â† Measure.map_apply hZ (measurableSet_singleton _)]
  have : Measure.map X Î¼ {g} = âˆ‘ x, (Measure.map Z Î¼ {x}) * (Measure.map X Î¼[|Z â»Â¹' {x}] {g}) := by
    simp_rw [Measure.map_apply hZ (measurableSet_singleton _)]
    have : Measure.map X Î¼ {g} = Measure.map X (âˆ‘ x, Î¼ (Z â»Â¹' {x}) â€¢ Î¼[|Z â»Â¹' {x}]) {g} := by
      rw [sum_meas_smul_cond_fiber hZ Î¼]
    rw [â† MeasureTheory.Measure.sum_fintype, Measure.map_sum hX.aemeasurable] at this
    simpa using this
  nth_rewrite 1 [this]
  rw [ENNReal.toReal_sum (by simp [ENNReal.mul_eq_top]), Finset.sum_mul, â† Finset.sum_add_distrib]
  congr with s
  rw [ENNReal.toReal_mul, mul_assoc, â† mul_add, â† mul_add]
  rcases eq_or_ne (Measure.map Z Î¼ {s}) 0 with hs | hs
  Â· simp [hs]
  rcases eq_or_ne (Measure.map X Î¼[|Z â»Â¹' {s}] {g}) 0 with hg | hg
  Â· simp [hg]
  have h'g : (Measure.map X Î¼[|Z â»Â¹' {s}] {g}).toReal â‰  0 := by
    simp [ENNReal.toReal_eq_zero_iff, hg]
  congr
  have hXg : Î¼.map X {g} â‰  0 := by
    intro h
    rw [this, Finset.sum_eq_zero_iff] at h
    specialize h s (Finset.mem_univ _)
    rw [mul_eq_zero] at h
    tauto
  have hXg' : (Î¼.map X {g}).toReal â‰  0 := by simp [ENNReal.toReal_eq_zero_iff, hXg]
  have hYg : Î¼'.map Y {g} â‰  0 := fun h â†¦ hXg (habs _ h)
  have hYg' : (Î¼'.map Y {g}).toReal â‰  0 := by simp [ENNReal.toReal_eq_zero_iff, hYg]
  rw [Real.log_div h'g hYg', Real.log_div hXg' hYg']
  abel

/-- `KL(X|Z â€– Y) â‰¥ 0`.-/
lemma condKLDiv_nonneg {S : Type*} [MeasurableSingletonClass G] [Fintype G]
    {X : Î© â†’ G} {Y : Î©' â†’ G} {Z : Î© â†’ S}
    [IsZeroOrProbabilityMeasure Î¼']
    (hX : Measurable X) (hY : Measurable Y)
    (habs : âˆ€ x, Î¼'.map Y {x} = 0 â†’ Î¼.map X {x} = 0) :
    0 â‰¤ KL[X | Z; Î¼ # Y ; Î¼'] := by
  rw [condKLDiv]
  refine tsum_nonneg (fun i â†¦ mul_nonneg (by simp) ?_)
  apply KLDiv_nonneg hX hY
  intro s hs
  specialize habs s hs
  rw [Measure.map_apply hX (measurableSet_singleton s)] at habs âŠ¢
  exact cond_absolutelyContinuous habs
