import Mathlib.Algebra.BigOperators.Group.Finset
import Mathlib.MeasureTheory.Integral.Prod
import PFR.Mathlib.Probability.IdentDistrib
import PFR.ForMathlib.Entropy.Group
import PFR.ForMathlib.Entropy.Kernel.RuzsaDist
import PFR.ForMathlib.ProbabilityMeasureProdCont

/-!
# Ruzsa distance

Here we define Ruzsa distance and establish its basic properties.

## Main definitions

* `rdist`: The Ruzsa distance $d[X ; Y]$ between two random variables
* `condRuzsaDist`: The conditional Ruzsa distance $d[X | Z ; Y | W]$ (or $d[X ; Y | W]$) between two
  random variables, conditioned on additional random variables

## Main results

* `rdist_triangle`: The Ruzsa triangle inequality for three random variables.
* `kaimanovich_vershik`. $$H[X + Y + Z] - H[X + Y] \leq H[Y+ Z] - H[Y]$$
* `ent_bsg`: If $Z=A+B$, then
  $$\sum_{z} P[Z=z] d[(A | Z = z) ; (B | Z = z)] \leq 3 I[A :B] + 2 H[Z] - H[A] - H[B]$$
-/

open Filter Function MeasureTheory Measure ProbabilityTheory
open scoped Topology

variable {Î© Î©' Î©'' Î©''' G S T : Type*}
  [mÎ© : MeasurableSpace Î©] {Î¼ : Measure Î©}
  [mÎ©' : MeasurableSpace Î©'] {Î¼' : Measure Î©'}
  [mÎ©'' : MeasurableSpace Î©''] {Î¼'' : Measure Î©''}
  [mÎ©''' : MeasurableSpace Î©'''] {Î¼''' : Measure Î©'''}
  [hG : MeasurableSpace G]

/-- Entropy depends continuously on the measure. -/
-- TODO: Use notation `Hm[Î¼]` here? (figure out how)
lemma continuous_measureEntropy_probabilityMeasure {Î© : Type*} [Fintype Î©]
    [TopologicalSpace Î©] [DiscreteTopology Î©] [MeasurableSpace Î©] [OpensMeasurableSpace Î©] :
    Continuous (fun (Î¼ : ProbabilityMeasure Î©) â†¦ measureEntropy (S := Î©) Î¼) := by
  unfold measureEntropy
  simp_rw [tsum_fintype]
  apply continuous_finset_sum
  intro Ï‰ _
  apply Real.continuous_negMulLog.comp
  simp only [measure_univ, inv_one, one_smul]
  exact continuous_probabilityMeasure_apply_of_isClopen (s := {Ï‰}) $ isClopen_discrete _

lemma continuous_entropy_restrict_probabilityMeasure [Fintype G]
    [TopologicalSpace G] [DiscreteTopology G] [BorelSpace G] :
    Continuous (fun (Î¼ : ProbabilityMeasure G) â†¦ H[id ; Î¼.toMeasure]) := by
  simp only [entropy_def, Measure.map_id]
  exact continuous_measureEntropy_probabilityMeasure

variable [AddCommGroup G]
  {X : Î© â†’ G} {Y : Î©' â†’ G} {Z : Î©'' â†’ G}

/-- The Ruzsa distance `rdist X Y` or `d[X ; Y]` between two random variables is defined as
`H[X'- Y'] - H[X']/2 - H[Y']/2`, where `X', Y'` are independent copies of `X, Y`. -/
noncomputable
def rdist (X : Î© â†’ G) (Y : Î©' â†’ G) (Î¼ : Measure Î© := by volume_tac)
    (Î¼' : Measure Î©' := by volume_tac) : â„ :=
  H[fun x â†¦ x.1 - x.2 ; (Î¼.map X).prod (Î¼'.map Y)] - H[X ; Î¼]/2 - H[Y ; Î¼']/2

/- Needed a new separator here, chose `#` arbitrarily, but am open to other suggestions -/
@[inherit_doc rdist] notation3:max "d[" X " ; " Î¼ " # " Y " ; " Î¼' "]" => rdist X Y Î¼ Î¼'

@[inherit_doc rdist] notation3:max "d[" X " # " Y "]" => rdist X Y volume volume

/-- Explicit formula for the Ruzsa distance. -/
lemma rdist_def (X : Î© â†’ G) (Y : Î©' â†’ G) (Î¼ : Measure Î©) (Î¼' : Measure Î©') :
    d[X ; Î¼ # Y ; Î¼']
      = H[fun x â†¦ x.1 - x.2 ; (Î¼.map X).prod (Î¼'.map Y)] - H[X ; Î¼]/2 - H[Y ; Î¼']/2 := rfl

/-- Ruzsa distance of random variables equals Ruzsa distance of the kernels. -/
lemma rdist_eq_rdistm : d[X ; Î¼ # Y ; Î¼'] = Kernel.rdistm (Î¼.map X) (Î¼'.map Y) := rfl

/-- Ruzsa distance depends continuously on the measure. -/
lemma continuous_rdist_restrict_probabilityMeasure [Fintype G]
    [TopologicalSpace G] [DiscreteTopology G] [BorelSpace G] :
    Continuous
      (fun (Î¼ : ProbabilityMeasure G Ã— ProbabilityMeasure G) â†¦
        d[id ; Î¼.1.toMeasure # id ; Î¼.2.toMeasure]) := by
  simp [rdist_def]
  have obsâ‚€ : Continuous (fun (Î¼ : ProbabilityMeasure G Ã— ProbabilityMeasure G) â†¦
      H[fun x â†¦ x.1 - x.2 ; Î¼.1.toMeasure.prod Î¼.2.toMeasure]) := by
    simp_rw [entropy_def]
    have diff_cts : Continuous (fun (x : G Ã— G) â†¦ x.1 - x.2) := by continuity
    have keyâ‚ := ProbabilityMeasure.continuous_prod_of_finite (Î± := G) (Î² := G)
    have keyâ‚‚ := ProbabilityMeasure.continuous_map diff_cts
    convert continuous_measureEntropy_probabilityMeasure.comp (keyâ‚‚.comp keyâ‚)
  have obsâ‚ : Continuous
      (fun (Î¼ : ProbabilityMeasure G Ã— ProbabilityMeasure G) â†¦ H[id ; Î¼.1.toMeasure]) := by
    convert (continuous_measureEntropy_probabilityMeasure (Î© := G)).comp continuous_fst
    simp [entropy_def]
  have obsâ‚‚ : Continuous
      (fun (Î¼ : ProbabilityMeasure G Ã— ProbabilityMeasure G) â†¦ H[id ; Î¼.2.toMeasure]) := by
    convert (continuous_measureEntropy_probabilityMeasure (Î© := G)).comp continuous_snd
    simp [entropy_def]
  continuity

lemma continuous_rdist_restrict_probabilityMeasureâ‚ [Fintype G]
    [TopologicalSpace G] [DiscreteTopology G] [BorelSpace G]
    (X : Î© â†’ G) (P : Measure Î© := by volume_tac) [IsProbabilityMeasure P] (X_mble : Measurable X) :
    Continuous
      (fun (Î¼ : ProbabilityMeasure G) â†¦ d[id ; P.map X # id ; Î¼.toMeasure]) := by
  have obs : IsProbabilityMeasure (P.map X) := by
    refine âŸ¨by simp [Measure.map_apply X_mble MeasurableSet.univ]âŸ©
  let Î¹ : ProbabilityMeasure G â†’ ProbabilityMeasure G Ã— ProbabilityMeasure G :=
      fun Î½ â†¦ âŸ¨âŸ¨P.map X, obsâŸ©, Î½âŸ©
  have Î¹_cont : Continuous Î¹ := Continuous.Prod.mk _
  convert continuous_rdist_restrict_probabilityMeasure.comp Î¹_cont

/-- Ruzsa distance between random variables equals Ruzsa distance between their distributions.-/
lemma rdist_eq_rdist_id_map : d[X ; Î¼ # Y ; Î¼'] = d[id ; Î¼.map X # id ; Î¼'.map Y] := by
  simp only [rdist_def, entropy_def, Measure.map_id]

/-- Ruzsa distance depends continuously on the second measure. -/
lemma continuous_rdist_restrict_probabilityMeasureâ‚' [Fintype G]
    [TopologicalSpace G] [DiscreteTopology G] [BorelSpace G]
    (X : Î© â†’ G) (P : Measure Î© := by volume_tac) [IsProbabilityMeasure P] (X_mble : Measurable X) :
    Continuous
      (fun (Î¼ : ProbabilityMeasure G) â†¦ d[X ; P # id ; Î¼.toMeasure]) := by
  simp only [@rdist_eq_rdist_id_map Î© G G mÎ© P hG, Measure.map_id]
  exact continuous_rdist_restrict_probabilityMeasureâ‚ _ _ X_mble

/-- If `X', Y'` are copies of `X, Y` respectively then `d[X' ; Y'] = d[X ; Y]`. -/
lemma ProbabilityTheory.IdentDistrib.rdist_eq {X' : Î©'' â†’ G} {Y' : Î©''' â†’ G}
    (hX : IdentDistrib X X' Î¼ Î¼'') (hY : IdentDistrib Y Y' Î¼' Î¼''') :
    d[X ; Î¼ # Y ; Î¼'] = d[X' ; Î¼'' # Y' ; Î¼'''] := by
  simp [rdist, hX.map_eq, hY.map_eq, hX.entropy_eq, hY.entropy_eq]

lemma tendsto_rdist_probabilityMeasure {Î± : Type*} {l : Filter Î±}
    [TopologicalSpace Î©] [BorelSpace Î©] [TopologicalSpace G] [BorelSpace G] [Fintype G]
    [DiscreteTopology G]
    {X Y : Î© â†’ G} (hX : Continuous X) (hY : Continuous Y)
    {Î¼ : Î± â†’ ProbabilityMeasure Î©} {Î½ : ProbabilityMeasure Î©} (hÎ¼ : Tendsto Î¼ l (ğ“ Î½)) :
    Tendsto (fun n â†¦ d[X ; (Î¼ n : Measure Î©) # Y ; (Î¼ n : Measure Î©)]) l
      (ğ“ (d[X ; Î½ # Y ; Î½])) := by
  have J (Î· : ProbabilityMeasure Î©) :
      d[X ; Î· # Y ; Î·] = d[(id : G â†’ G) ; Î·.map hX.aemeasurable # id ; Î·.map hY.aemeasurable] := by
    apply ProbabilityTheory.IdentDistrib.rdist_eq
    Â· exact âŸ¨hX.aemeasurable, aemeasurable_id, by simpâŸ©
    Â· exact âŸ¨hY.aemeasurable, aemeasurable_id, by simpâŸ©
  simp_rw [J]
  have Z := ((continuous_rdist_restrict_probabilityMeasure (G := G)).tendsto
    ((Î½.map hX.aemeasurable), (Î½.map hY.aemeasurable)))
  have T : Tendsto (fun n â†¦ (((Î¼ n).map hX.aemeasurable), ((Î¼ n).map hY.aemeasurable)))
      l (ğ“ (((Î½.map hX.aemeasurable), (Î½.map hY.aemeasurable)))) := by
    apply Tendsto.prod_mk_nhds
    Â· exact ProbabilityMeasure.tendsto_map_of_tendsto_of_continuous Î¼ Î½ hÎ¼ hX
    Â· exact ProbabilityMeasure.tendsto_map_of_tendsto_of_continuous Î¼ Î½ hÎ¼ hY
  apply Z.comp T

section rdist

variable [Countable G] [MeasurableSingletonClass G]

/-- If `X, Y` are independent `G`-random variables then `d[X ; Y] = H[X - Y] - H[X]/2 - H[Y]/2`. -/
lemma ProbabilityTheory.IndepFun.rdist_eq [IsFiniteMeasure Î¼]
    {Y : Î© â†’ G} (h : IndepFun X Y Î¼) (hX : Measurable X) (hY : Measurable Y) :
    d[X ; Î¼ # Y ; Î¼] = H[X - Y ; Î¼] - H[X ; Î¼]/2 - H[Y ; Î¼]/2 := by
  rw [rdist_def]
  congr 2
  have h_prod : (Î¼.map X).prod (Î¼.map Y) = Î¼.map (âŸ¨X, YâŸ©) :=
    ((indepFun_iff_map_prod_eq_prod_map_map hX.aemeasurable hY.aemeasurable).mp h).symm
  rw [h_prod, entropy_def, Measure.map_map (measurable_fst.sub measurable_snd) (hX.prod_mk hY)]
  rfl

/-- `d[X ; Y] â‰¤ H[X]/2 + H[Y]/2`. -/
lemma rdist_le_avg_ent {X : Î© â†’ G} {Y : Î©' â†’ G} [FiniteRange X] [FiniteRange Y] (hX : Measurable X)
    (hY : Measurable Y) (Î¼ : Measure Î© := by volume_tac) (Î¼' : Measure Î©' := by volume_tac)
  [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î¼'] :
    d[X ; Î¼ # Y ; Î¼'] â‰¤ (H[X ; Î¼] + H[Y ; Î¼'])/2 := by
  rcases ProbabilityTheory.independent_copies_finiteRange hX hY Î¼ Î¼'
    with âŸ¨Î½, X', Y', hprob, hX', hY', h_indep, hidentX, hidentY, hfinX, hfinYâŸ©
  rw [â† ProbabilityTheory.IdentDistrib.rdist_eq hidentX hidentY,
    â† IdentDistrib.entropy_eq hidentX, â† IdentDistrib.entropy_eq hidentY,
    ProbabilityTheory.IndepFun.rdist_eq h_indep hX' hY']
  suffices H[X' - Y'; Î½] â‰¤ H[X'; Î½] + H[Y'; Î½] by linarith
  change H[(fun x â†¦ x.1 - x.2) âˆ˜ âŸ¨X',Y' âŸ©; Î½] â‰¤ H[X'; Î½] + H[Y'; Î½]
  exact ((entropy_comp_le Î½ (by fun_prop) _).trans) (entropy_pair_le_add hX' hY' Î½)

/-- Applying an injective homomorphism does not affect Ruzsa distance. -/
lemma rdist_of_inj {H : Type*} [hH : MeasurableSpace H] [MeasurableSingletonClass H]
  [AddCommGroup H] [Countable H] (hX : Measurable X) (hY : Measurable Y)
  (Ï† : G â†’+ H) (hÏ† : Injective Ï†) [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î¼' ]:
    d[Ï† âˆ˜ X ; Î¼ # Ï† âˆ˜ Y ; Î¼'] = d[X ; Î¼ # Y ; Î¼'] := by
    rw [rdist_def, rdist_def]
    congr 2
    Â· rw [â† entropy_comp_of_injective _ (by fun_prop) _ hÏ†]
      apply IdentDistrib.entropy_eq
      constructor
      Â· exact Measurable.aemeasurable .of_discrete
      Â· exact Measurable.aemeasurable .of_discrete
      set g := fun x : H Ã— H â†¦ x.1 - x.2
      set f := fun x : G Ã— G â†¦ (Ï† x.1, Ï† x.2)
      have : Ï† âˆ˜ (fun x â†¦ x.1 - x.2) = g âˆ˜ f := by
        ext x
        simp
      rw [this, â† MeasureTheory.Measure.map_map (g := g) (f := f) .of_discrete
        .of_discrete, â† MeasureTheory.Measure.map_map .of_discrete hX,
        â† MeasureTheory.Measure.map_map .of_discrete hY]
      congr
      convert Measure.map_prod_map _ _ .of_discrete .of_discrete
      Â· exact instSFiniteMap Î¼ X
      Â· exact instSFiniteMap Î¼' Y
      all_goals infer_instance
    Â· congr 1
      exact entropy_comp_of_injective _ hX _ hÏ†
    exact entropy_comp_of_injective _ hY _ hÏ†

/-- `d[X ; 0] = H[X] / 2`. -/
lemma rdist_zero_eq_half_ent [IsFiniteMeasure Î¼] [IsProbabilityMeasure Î¼'] :
    d[X ; Î¼ # fun _ â†¦ 0 ; Î¼'] = H[X ; Î¼]/2 := by
  have aux : H[fun x => x.1 - x.2 ; Measure.prod (Measure.map X Î¼) (Measure.map (fun x => 0) Î¼')]
            = H[X ; Î¼] := by
    have h: Measure.map (fun x => x.1 - x.2)
                        (Measure.prod (Measure.map X Î¼) (Measure.map (fun x => 0) Î¼'))
            = Measure.map X Î¼ := by
              simp [MeasureTheory.Measure.map_const, MeasureTheory.Measure.prod_dirac]
              rw [Measure.map_map (by fun_prop) (by fun_prop)]
              have helper : ((fun (x : G Ã— G) => x.1 - x.2) âˆ˜ fun x => (x, (0 : G))) = id := by
                funext; simp
              rw [helper, Measure.map_id]
    simp [entropy_def, h]
  simp [rdist_def, entropy_const (0 : G), aux]
  ring

/-- `d[X ; Y] = d[Y ; X]` -/
lemma rdist_symm [IsFiniteMeasure Î¼] [IsFiniteMeasure Î¼'] :
    d[X ; Î¼ # Y ; Î¼'] = d[Y ; Î¼' # X ; Î¼] := by
  rw [rdist_def, rdist_def, sub_sub, sub_sub, add_comm]
  congr 1
  rw [â† entropy_neg (measurable_fst.sub measurable_snd)]
  have : (-fun x : G Ã— G â†¦ x.1 - x.2) = (fun x â†¦ x.1 - x.2) âˆ˜ Prod.swap := by ext ; simp
  rw [this, entropy_def, â† Measure.map_map (measurable_fst.sub measurable_snd) measurable_swap,
    Measure.prod_swap]
  rfl

variable [FiniteRange X] [FiniteRange Y]

/-- `|H[X] - H[Y]| â‰¤ 2 d[X ; Y]`. -/
lemma diff_ent_le_rdist [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î¼']
    (hX : Measurable X) (hY : Measurable Y) :
    |H[X ; Î¼] - H[Y ; Î¼']| â‰¤ 2 * d[X ; Î¼ # Y ; Î¼'] := by
  obtain âŸ¨Î½, X', Y', _, hX', hY', hind, hIdX, hIdY, _, _âŸ© := independent_copies_finiteRange hX hY Î¼ Î¼'
  rw [â† hIdX.rdist_eq hIdY, hind.rdist_eq hX' hY', â† hIdX.entropy_eq, â† hIdY.entropy_eq, abs_le]
  have := max_entropy_le_entropy_sub hX' hY' hind
  constructor
  Â· linarith[le_max_right H[X'; Î½] H[Y'; Î½]]
  Â· linarith[le_max_left H[X'; Î½] H[Y'; Î½]]

/-- `H[X - Y] - H[X] â‰¤ 2d[X ; Y]`. -/
lemma diff_ent_le_rdist' [IsProbabilityMeasure Î¼] {Y : Î© â†’ G}
    (hX : Measurable X) (hY : Measurable Y) (h : IndepFun X Y Î¼) [FiniteRange Y]:
    H[X - Y ; Î¼] - H[X ; Î¼] â‰¤ 2 * d[X ; Î¼ # Y ; Î¼] := by
  rw [h.rdist_eq hX hY]
  linarith[max_entropy_le_entropy_sub hX hY h, le_max_right H[X ; Î¼] H[Y; Î¼]]

/-- `H[X - Y] - H[Y] â‰¤ 2d[X ; Y]`. -/
lemma diff_ent_le_rdist'' [IsProbabilityMeasure Î¼] {Y : Î© â†’ G}
    (hX : Measurable X) (hY : Measurable Y) (h : IndepFun X Y Î¼) [FiniteRange Y]:
    H[X - Y ; Î¼] - H[Y ; Î¼] â‰¤ 2 * d[X ; Î¼ # Y ; Î¼] := by
  rw [h.rdist_eq hX hY]
  linarith[max_entropy_le_entropy_sub hX hY h, le_max_left H[X ; Î¼] H[Y; Î¼]]

/-- `d[X ; Y] â‰¥ 0`. -/
lemma rdist_nonneg [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î¼']
    (hX : Measurable X) (hY : Measurable Y) : 0 â‰¤ d[X ; Î¼ # Y ; Î¼'] := by
  linarith [ge_trans (diff_ent_le_rdist hX hY) (abs_nonneg (H[X ; Î¼] - H[Y ; Î¼']))]

/-- If $G$ is an additive group and $X$ is a $G$-valued random variable and
$H\leq G$ is a finite subgroup then, with $\pi:G\to G/H$ the natural homomorphism we have
(where $U_H$ is uniform on $H$) $\mathbb{H}(\pi(X))\leq 2d[X;U_H].$ -/
lemma ent_of_proj_le {UH: Î©' â†’ G} [FiniteRange UH]
    [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î¼']
    (hX : Measurable X) (hU : Measurable UH) {H : AddSubgroup G} (hH : Set.Finite (H : Set G)) -- TODO: infer from [FiniteRange UH]?
    (hunif : IsUniform H UH Î¼') :
    H[(QuotientAddGroup.mk' H) âˆ˜ X; Î¼] â‰¤ 2 * d[X; Î¼ # UH ; Î¼'] := by
  obtain âŸ¨Î½, X', UH', hÎ½, hX', hUH', h_ind, h_id_X', h_id_UH', _, _âŸ© :=
    independent_copies_finiteRange hX hU Î¼ Î¼'
  replace hunif : IsUniform H UH' Î½ :=
    IsUniform.of_identDistrib hunif h_id_UH'.symm .of_discrete
  rewrite [â† (h_id_X'.comp (by fun_prop)).entropy_eq, â† h_id_X'.rdist_eq h_id_UH']
  let Ï€ := â‡‘(QuotientAddGroup.mk' H)
  let Î½q := Measure.map (Ï€ âˆ˜ X') Î½
  haveI : Countable (HasQuotient.Quotient G H) := Quotient.countable
  haveI : MeasurableSingletonClass (HasQuotient.Quotient G H) :=
    { measurableSet_singleton := fun _ â†¦ measurableSet_quotient.mpr .of_discrete }
  have : Finite H := hH
  have : H[X' - UH' | Ï€ âˆ˜ X' ; Î½] = H[UH' ; Î½] := by
    have h_meas_le : âˆ€ y âˆˆ FiniteRange.toFinset (Ï€ âˆ˜ X'),
        (Î½q {y}).toReal * H[X' - UH' | (Ï€ âˆ˜ X') â† y ; Î½] â‰¤ (Î½q {y}).toReal * H[UH' ; Î½] := by
      intro x _
      refine mul_le_mul_of_nonneg_left ?_ ENNReal.toReal_nonneg
      let Î½' := Î½[|Ï€ âˆ˜ X' â† x]
      let Ï€' := QuotientAddGroup.mk (s := H)
      have h_card : Nat.card (Ï€' â»Â¹' {x}) = Nat.card H := Nat.card_congr <|
        (QuotientAddGroup.preimageMkEquivAddSubgroupProdSet H _).trans <| Equiv.prodUnique H _
      haveI : Finite (Ï€' â»Â¹' {x}) :=
        Nat.finite_of_card_ne_zero <| h_card.trans_ne <| Nat.pos_iff_ne_zero.mp (Nat.card_pos)
      let H_x := (Ï€' â»Â¹' {x}).toFinite.toFinset
      have h : âˆ€áµ Ï‰ âˆ‚Î½', (X' - UH') Ï‰ âˆˆ H_x := by
        let T : Set (G Ã— G) := ((Ï€' âˆ˜ X') â»Â¹' {x})á¶œ
        let U : Set (G Ã— G) := UH' â»Â¹' Há¶œ
        have h_subset : (X' - UH') â»Â¹' H_xá¶œ âŠ† T âˆª U :=
          fun Ï‰ hÏ‰ â†¦ Classical.byContradiction fun h â†¦ by simp_all [not_or, T, U, H_x, Ï€']
        refine MeasureTheory.mem_ae_iff.mpr (le_zero_iff.mp ?_)
        calc
          _ â‰¤ Î½' T + Î½' U := (measure_mono h_subset).trans (measure_union_le T U)
          _ = Î½' T + 0 := congrArg _ <| by
            simp only [Î½', ProbabilityTheory.cond, Measure.smul_apply, smul_eq_mul]
            rw [le_zero_iff.mp <| (restrict_apply_le _ U).trans_eq hunif.measure_preimage_compl,
              mul_zero]
          _ = 0 := (add_zero _).trans <| by
            have : restrict Î½ (Ï€ âˆ˜ X' â»Â¹' {x}) T = 0 := by
              simp [restrict_apply .of_discrete, T, Ï€', Ï€]
            simp only [Î½', ProbabilityTheory.cond, Measure.smul_apply, smul_eq_mul]
            rw [this, mul_zero]
      convert entropy_le_log_card_of_mem (Measurable.sub hX' hUH') h
      simp_rw [hunif.entropy_eq' hH hUH', H_x, Set.Finite.mem_toFinset, h_card,
        SetLike.coe_sort_coe]
    have h_one : (âˆ‘ x in FiniteRange.toFinset (Ï€ âˆ˜ X'), (Î½q {x}).toReal) = 1 := by
      rewrite [Finset.sum_toReal_measure_singleton]
      apply (ENNReal.toReal_eq_one_iff _).mpr
      haveI := isProbabilityMeasure_map <| (Measurable.of_discrete (f := Ï€ âˆ˜ X')).aemeasurable
        (Î¼ := Î½)
      rewrite [â† measure_univ (Î¼ := Î½q), â† FiniteRange.range]
      let rng := Set.range (Ï€ âˆ˜ X')
      have h_compl : Î½q rngá¶œ = 0 := ae_map_mem_range (Ï€ âˆ˜ X') .of_discrete Î½
      rw [â† MeasureTheory.measure_add_measure_compl (MeasurableSet.of_discrete (s := rng)),
        h_compl, add_zero]
    haveI := FiniteRange.sub X' UH'
    have h_ge : H[X' - UH' | Ï€ âˆ˜ X' ; Î½] â‰¥ H[UH' ; Î½] := calc
      _ â‰¥ H[X' - UH' | X' ; Î½] := condEntropy_comp_ge Î½ hX' (hX'.sub hUH') Ï€
      _ = H[UH' | X' ; Î½] := condEntropy_sub_left hUH' hX'
      _ = H[UH' ; Î½] := h_ind.symm.condEntropy_eq_entropy hUH' hX'
    have h_le : H[X' - UH' | Ï€ âˆ˜ X' ; Î½] â‰¤ H[UH' ; Î½] := by
      rewrite [condEntropy_eq_sum _ _ _ .of_discrete]
      apply (Finset.sum_le_sum h_meas_le).trans
      rewrite [â† Finset.sum_mul, h_one, one_mul]
      rfl
    exact h_le.ge_iff_eq.mp h_ge
  have : H[X' - UH' ; Î½] = H[Ï€ âˆ˜ X' ; Î½] + H[UH' ; Î½] := by calc
    _ = H[âŸ¨X' - UH', Ï€ âˆ˜ (X' - UH')âŸ© ; Î½] := (entropy_prod_comp (hX'.sub hUH') Î½ Ï€).symm
    _ = H[âŸ¨X' - UH', Ï€ âˆ˜ X'âŸ© ; Î½] := by
      apply IdentDistrib.entropy_eq <| IdentDistrib.of_ae_eq (Measurable.aemeasurable
        .of_discrete) <| MeasureTheory.mem_ae_iff.mpr _
      convert hunif.measure_preimage_compl
      ext; simp [Ï€]
    _ = H[Ï€ âˆ˜ X' ; Î½] + H[UH' ; Î½] := by
      rewrite [chain_rule Î½ (by exact hX'.sub hUH') .of_discrete]
      congr
  have : d[X' ; Î½ # UH' ; Î½] = H[Ï€ âˆ˜ X' ; Î½] + (H[UH' ; Î½] - H[X' ; Î½]) / 2 := by
    rewrite [h_ind.rdist_eq hX' hUH']
    linarith only [this]
  linarith only [this, (abs_le.mp (diff_ent_le_rdist hX' hUH' (Î¼ := Î½) (Î¼' := Î½))).2]

/-- Adding a constant to a random variable does not change the Rusza distance. -/
lemma rdist_add_const [IsZeroOrProbabilityMeasure Î¼] [IsZeroOrProbabilityMeasure Î¼']
    (hX : Measurable X) (hY : Measurable Y) {c} :
    d[X ; Î¼ # Y + fun _ â†¦ c; Î¼'] = d[X ; Î¼ # Y ; Î¼'] := by
  rcases eq_zero_or_isProbabilityMeasure Î¼ with rfl | hÎ¼
  Â· simp [rdist_def, entropy_add_const hY]
  rcases eq_zero_or_isProbabilityMeasure Î¼' with rfl | hÎ¼'
  Â· simp [rdist_def]
  obtain âŸ¨Î½, X', Y', _, hX', hY', hind, hIdX, hIdY, _, _âŸ© := independent_copies_finiteRange hX hY Î¼ Î¼'
  have A : IdentDistrib (Y' + fun _ â†¦ c) (Y + fun _ â†¦ c) Î½ Î¼' := by
    change IdentDistrib (fun Ï‰ â†¦ Y' Ï‰ + c) (fun Ï‰ â†¦ Y Ï‰ + c) Î½ Î¼'
    apply hIdY.comp (measurable_add_const c)
  have B : IndepFun X' (Y' + fun _ â†¦ c) Î½ := by
    change IndepFun X' (fun Ï‰ â†¦ Y' Ï‰ + c) Î½
    apply hind.comp measurable_id (measurable_add_const c)
  have C : X' - (Y' + fun _ â†¦ c) = (X' - Y') + (fun _ â†¦ -c) := by
    ext Ï‰; simp; abel
  rw [â† hIdX.rdist_eq hIdY, â† hIdX.rdist_eq A, hind.rdist_eq hX' hY',
    B.rdist_eq hX' (hY'.add_const _), entropy_add_const hY' c, C, entropy_add_const]
  exact hX'.sub hY'

/-- A variant of `rdist_add_const` where one adds constants to both variables. -/
lemma rdist_add_const' [IsZeroOrProbabilityMeasure Î¼] [IsZeroOrProbabilityMeasure Î¼']
    (c : G) (c' : G) (hX : Measurable X) (hY : Measurable Y) :
    d[X + fun _ â†¦ c; Î¼ # Y + fun _ â†¦ c'; Î¼'] = d[X ; Î¼ # Y ; Î¼'] := by
  rw [rdist_add_const _ hY, rdist_symm, rdist_add_const hY hX, rdist_symm]
  fun_prop

/-- The **improved entropic Ruzsa triangle inequality**. -/
lemma ent_of_diff_le (X : Î© â†’ G) (Y : Î© â†’ G) (Z : Î© â†’ G)
    (hX : Measurable X) (hY : Measurable Y) (hZ : Measurable Z)
    (h : IndepFun (âŸ¨X, YâŸ©) Z Î¼)
    [IsProbabilityMeasure Î¼] [FiniteRange X] [FiniteRange Y] [FiniteRange Z] :
    H[X - Y; Î¼] â‰¤ H[X - Z; Î¼] + H[Z - Y; Î¼] - H[Z; Î¼] := by
  have h1 : H[âŸ¨X - Z, âŸ¨Y, X - YâŸ©âŸ©; Î¼] + H[X - Y; Î¼] â‰¤ H[âŸ¨X - Z, X - YâŸ©; Î¼] + H[âŸ¨Y, X - YâŸ©; Î¼] :=
    entropy_triple_add_entropy_le Î¼ (hX.sub hZ) hY (hX.sub hY)
  have h2 : H[âŸ¨X - Z, X - YâŸ© ; Î¼] â‰¤ H[X - Z ; Î¼] + H[Y - Z ; Î¼] := by
    calc H[âŸ¨X - Z, X - YâŸ© ; Î¼] â‰¤ H[âŸ¨X - Z, Y - ZâŸ© ; Î¼] := by
          have : âŸ¨X - Z, X - YâŸ© = (fun p â†¦ (p.1, p.1 - p.2)) âˆ˜ âŸ¨X - Z, Y - ZâŸ© := by ext1; simp
          rw [this]
          apply entropy_comp_le Î¼ (by fun_prop)
    _ â‰¤ H[X - Z ; Î¼] + H[Y - Z ; Î¼] := by
          have h : 0 â‰¤ H[X - Z ; Î¼] + H[Y - Z ; Î¼] - H[âŸ¨X - Z, Y - ZâŸ© ; Î¼] := by
            apply mutualInfo_nonneg (by fun_prop) (by fun_prop) Î¼
          linarith
  have h3 : H[âŸ¨Y, X - YâŸ© ; Î¼] â‰¤ H[âŸ¨X, YâŸ© ; Î¼] := by
    have : âŸ¨Y, X - YâŸ© = (fun p â†¦ (p.2, p.1 - p.2)) âˆ˜ âŸ¨X, YâŸ© := by ext1; simp
    rw [this]
    exact entropy_comp_le Î¼ (hX.prod_mk hY) _
  have h4 : H[âŸ¨X - Z, âŸ¨Y, X - YâŸ©âŸ©; Î¼] = H[âŸ¨X, âŸ¨Y, ZâŸ©âŸ© ; Î¼] := by
    refine entropy_of_comp_eq_of_comp Î¼ ((hX.sub hZ).prod_mk (hY.prod_mk (hX.sub hY)))
      (hX.prod_mk (hY.prod_mk hZ))
      (fun p : G Ã— (G Ã— G) â†¦ (p.2.2 + p.2.1, p.2.1, -p.1 + p.2.2 + p.2.1))
      (fun p : G Ã— G Ã— G â†¦ (p.1 - p.2.2, p.2.1, p.1 - p.2.1)) ?_ ?_
    Â· ext1; simp
    Â· ext1; simp
  have h5 : H[âŸ¨X, âŸ¨Y, ZâŸ©âŸ© ; Î¼] = H[âŸ¨X, YâŸ© ; Î¼] + H[Z ; Î¼] := by
    rw [entropy_assoc hX hY hZ, entropy_pair_eq_add (hX.prod_mk hY) hZ]
    exact h
  rw [h4, h5] at h1
  calc H[X - Y; Î¼] â‰¤ H[X - Z; Î¼] + H[Y - Z; Î¼] - H[Z; Î¼] := by linarith
  _ = H[X - Z; Î¼] + H[Z - Y; Î¼] - H[Z; Î¼] := by
    congr 2
    rw [entropy_sub_comm hY hZ]

/-- The **entropic Ruzsa triangle inequality** -/
lemma rdist_triangle {X : Î© â†’ G} {Y : Î©' â†’ G} {Z : Î©'' â†’ G}
    (hX : Measurable X) (hY : Measurable Y) (hZ : Measurable Z)
    [hÎ¼ : IsProbabilityMeasure Î¼] [hÎ¼' : IsProbabilityMeasure Î¼']
    [hÎ¼'' : IsProbabilityMeasure Î¼''] [FiniteRange X] [FiniteRange Y] [FiniteRange Z] :
    d[X ; Î¼ # Z ; Î¼''] â‰¤ d[X ; Î¼ # Y ; Î¼'] + d[Y ; Î¼' # Z ; Î¼''] := by
  obtain âŸ¨A, mA, Î¼A, X', Y', Z', hÎ¼A, hInd, hX', hY', hZ', HX, HY, HZ, _, _, _âŸ© :=
    independent_copies3_nondep_finiteRange hX hY hZ Î¼ Î¼' Î¼''
  suffices d[X' ; Î¼A # Z' ; Î¼A] â‰¤ d[X' ; Î¼A # Y' ; Î¼A] + d[Y' ; Î¼A # Z' ; Î¼A] by
    rwa [HX.rdist_eq HY, HY.rdist_eq HZ, HX.rdist_eq HZ] at this
  have IndepLem : IndepFun (âŸ¨X', Z'âŸ©) Y' Î¼A :=
    iIndepFun.indepFun_prod_mk hInd (fun i => by fin_cases i ; all_goals { simpa }) 0 2 1
      (by norm_cast) (by norm_cast)
  calc d[X' ; Î¼A # Z' ; Î¼A] = H[X' - Z'; Î¼A] - (H[X'; Î¼A] / 2 + H[Z'; Î¼A] / 2) := by
        rw [ProbabilityTheory.IndepFun.rdist_eq
          (by simpa using hInd.indepFun (show 0 â‰  2 by norm_cast)) hX' hZ'] ; ring
    _ â‰¤ (H[X' - Y' ; Î¼A] + H[Y' - Z' ; Î¼A] - H[Y' ; Î¼A]) - (H[X'; Î¼A] / 2 + H[Z'; Î¼A] / 2) :=
          sub_le_sub_right (ent_of_diff_le _ _ _ hX' hZ' hY' IndepLem) _
    _ = (H[X' - Y' ; Î¼A] - H[X'; Î¼A] / 2 - H[Y' ; Î¼A] / 2) +
          (H[Y' - Z' ; Î¼A] - H[Y' ; Î¼A] / 2 - H[Z'; Î¼A] / 2) := by ring
    _ = d[X' ; Î¼A # Y' ; Î¼A] + d[Y' ; Î¼A # Z' ; Î¼A] := by
        rw [ProbabilityTheory.IndepFun.rdist_eq (by simpa using (iIndepFun.indepFun hInd
          (show 0 â‰  1 by norm_cast))) hX' hY', ProbabilityTheory.IndepFun.rdist_eq
          (by simpa using (iIndepFun.indepFun hInd (show 1 â‰  2 by norm_cast))) hY' hZ']

end rdist

variable [MeasurableSpace S] [MeasurableSpace T] [Countable G] [MeasurableSingletonClass G]

/-- The conditional Ruzsa distance `d[X|Z ; Y|W]`. -/
noncomputable
def condRuzsaDist (X : Î© â†’ G) (Z : Î© â†’ S) (Y : Î©' â†’ G) (W : Î©' â†’ T)
    (Î¼ : Measure Î© := by volume_tac) [IsFiniteMeasure Î¼]
    (Î¼' : Measure Î©' := by volume_tac) [IsFiniteMeasure Î¼'] : â„ :=
  dk[condDistrib X Z Î¼ ; Î¼.map Z # condDistrib Y W Î¼' ; Î¼'.map W]

@[inherit_doc condRuzsaDist]
notation3:max "d[" X " | " Z " ; " Î¼ " # " Y " | " W " ; " Î¼'"]" => condRuzsaDist X Z Y W Î¼ Î¼'

@[inherit_doc condRuzsaDist]
notation3:max "d[" X " | " Z " # " Y " | " W "]" => condRuzsaDist X Z Y W volume volume

lemma condRuzsaDist_def (X : Î© â†’ G) (Z : Î© â†’ S) (Y : Î©' â†’ G) (W : Î©' â†’ T)
    (Î¼ : Measure Î©) [IsFiniteMeasure Î¼] (Î¼' : Measure Î©') [IsFiniteMeasure Î¼'] :
    d[X | Z ; Î¼ # Y | W ; Î¼']
      = dk[condDistrib X Z Î¼ ; Î¼.map Z # condDistrib Y W Î¼' ; Î¼'.map W] := rfl

@[simp] lemma condRuszaDist_zero_right (X : Î© â†’ G) (Z : Î© â†’ S) (Y : Î©' â†’ G) (W : Î©' â†’ T)
    (Î¼ : Measure Î©) [IsFiniteMeasure Î¼] :
    d[X | Z ; Î¼ # Y | W ; 0] = 0 := by
  simp only [condRuzsaDist, aemeasurable_zero_measure, not_true_eq_false, Measure.map_zero,
    Kernel.rdist_zero_right]

@[simp] lemma condRuszaDist_zero_left (X : Î© â†’ G) (Z : Î© â†’ S) (Y : Î©' â†’ G) (W : Î©' â†’ T)
    (Î¼' : Measure Î©') [IsFiniteMeasure Î¼'] :
    d[X | Z ; 0 # Y | W ; Î¼'] = 0 := by
  simp [condRuzsaDist]

variable [MeasurableSingletonClass S] [MeasurableSingletonClass T]

/-- Explicit formula for conditional Ruzsa distance $d[X|Z; Y|W]$ in a fintype. -/
lemma condRuzsaDist_eq_sum' {X : Î© â†’ G} {Z : Î© â†’ S} {Y : Î©' â†’ G} {W : Î©' â†’ T}
    (hX : Measurable X) (hZ : Measurable Z) (hY : Measurable Y) (hW : Measurable W)
    (Î¼ : Measure Î©) [IsFiniteMeasure Î¼] (Î¼' : Measure Î©') [IsFiniteMeasure Î¼']
    [Fintype S] [Fintype T] :
    d[X | Z ; Î¼ # Y | W ; Î¼']
      = âˆ‘ z, âˆ‘ w, (Î¼ (Z â»Â¹' {z})).toReal * (Î¼' (W â»Â¹' {w})).toReal
          * d[X ; (Î¼[|Z â† z]) # Y ; (Î¼'[|W â† w])] := by
  rw [condRuzsaDist_def, Kernel.rdist, integral_fintype _ .of_finite]
  simp_rw [Measure.prod_apply_singleton, ENNReal.toReal_mul, smul_eq_mul, Fintype.sum_prod_type,
    Measure.map_apply hZ (.singleton _),
    Measure.map_apply hW (.singleton _)]
  congr with z
  congr with w
  by_cases hz : Î¼ (Z â»Â¹' {z}) = 0
  Â· simp only [mul_eq_mul_left_iff, mul_eq_zero]
    refine Or.inr (Or.inl ?_)
    simp [ENNReal.toReal_eq_zero_iff, measure_ne_top Î¼, hz]
  by_cases hw : Î¼' (W â»Â¹' {w}) = 0
  Â· simp only [mul_eq_mul_left_iff, mul_eq_zero]
    refine Or.inr (Or.inr ?_)
    simp [ENNReal.toReal_eq_zero_iff, measure_ne_top Î¼', hw]
  congr 1
  rw [rdist_eq_rdistm]
  rw [condDistrib_apply hX hZ _ _ hz, condDistrib_apply hY hW _ _ hw]

/-- Explicit formula for conditional Ruzsa distance $d[X|Z; Y|W]$. -/
lemma condRuzsaDist_eq_sum {X : Î© â†’ G} {Z : Î© â†’ S} {Y : Î©' â†’ G} {W : Î©' â†’ T}
    (hX : Measurable X) (hZ : Measurable Z) (hY : Measurable Y) (hW : Measurable W)
    (Î¼ : Measure Î©) [IsFiniteMeasure Î¼] (Î¼' : Measure Î©') [IsFiniteMeasure Î¼']
    [FiniteRange Z] [FiniteRange W] :
    d[X | Z ; Î¼ # Y | W ; Î¼']
      = âˆ‘ z in FiniteRange.toFinset Z, âˆ‘ w in FiniteRange.toFinset W,
        (Î¼ (Z â»Â¹' {z})).toReal * (Î¼' (W â»Â¹' {w})).toReal
          * d[X ; (Î¼[|Z â† z]) # Y ; (Î¼'[|W â† w])] := by
  have : Measure.prod (Î¼.map Z) (Î¼'.map W) ((((FiniteRange.toFinset Z)
      Ã—Ë¢ (FiniteRange.toFinset W)) : Finset (S Ã— T)): Set (S Ã— T))á¶œ = 0 := by
    apply Measure.prod_of_full_measure_finset
    all_goals {
      rw [Measure.map_apply â€¹_â€º]
      convert measure_empty (Î¼ := Î¼)
      simp [â† FiniteRange.range]
      measurability
    }
  rw [condRuzsaDist_def, Kernel.rdist, integral_eq_setIntegral this, integral_finset _ _ IntegrableOn.finset]
  simp_rw [Measure.prod_apply_singleton, ENNReal.toReal_mul, smul_eq_mul, Finset.sum_product,
    Measure.map_apply hZ (.singleton _),
    Measure.map_apply hW (.singleton _)]
  congr with z
  congr with w
  by_cases hz : Î¼ (Z â»Â¹' {z}) = 0
  Â· simp only [mul_eq_mul_left_iff, mul_eq_zero]
    refine Or.inr (Or.inl ?_)
    simp [ENNReal.toReal_eq_zero_iff, measure_ne_top Î¼, hz]
  by_cases hw : Î¼' (W â»Â¹' {w}) = 0
  Â· simp only [mul_eq_mul_left_iff, mul_eq_zero]
    refine Or.inr (Or.inr ?_)
    simp [ENNReal.toReal_eq_zero_iff, measure_ne_top Î¼', hw]
  congr 1
  rw [rdist_eq_rdistm]
  rw [condDistrib_apply hX hZ _ _ hz, condDistrib_apply hY hW _ _ hw]

variable [Countable S]

/-- $$ d[X|Z; Y|W] = d[Y|W; X|Z]$$-/
lemma condRuzsaDist_symm [Countable T]
    {X : Î© â†’ G} {Z : Î© â†’ S} {Y : Î©' â†’ G} {W : Î©' â†’ T} (hZ : Measurable Z)
    (hW : Measurable W) [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î¼'] [FiniteRange Z]
    [FiniteRange W] :
    d[X | Z ; Î¼ # Y | W ; Î¼'] = d[Y | W ; Î¼' # X | Z ; Î¼] := by
  have : IsProbabilityMeasure (Î¼.map Z) := isProbabilityMeasure_map hZ.aemeasurable
  have : IsProbabilityMeasure (Î¼'.map W) := isProbabilityMeasure_map hW.aemeasurable
  rw [condRuzsaDist_def, condRuzsaDist_def, Kernel.rdist_symm]

lemma condRuzsaDist_nonneg [Countable T] {X : Î© â†’ G} (hX : Measurable X) [FiniteRange X]
    {Z : Î© â†’ S} (hZ : Measurable Z) [FiniteRange Z]
    {Y : Î©' â†’ G} (hY : Measurable Y) [FiniteRange Y]
    {W : Î©' â†’ T} (hW : Measurable W) [FiniteRange W]
    [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î¼'] :
    0 â‰¤ d[X | Z ; Î¼ # Y | W ; Î¼'] := by
  rw [condRuzsaDist_def]
  have : IsProbabilityMeasure (Î¼.map Z) := isProbabilityMeasure_map hZ.aemeasurable
  have : IsProbabilityMeasure (Î¼'.map W) := isProbabilityMeasure_map hW.aemeasurable
  refine Kernel.rdist_nonneg ?_ ?_
  Â· exact Kernel.aefiniteKernelSupport_condDistrib _ _ _ hX hZ
  Â· exact Kernel.aefiniteKernelSupport_condDistrib _ _ _ hY hW

/-- The conditional Ruzsa distance `d[X ; Y|W]`. -/
noncomputable
def condRuzsaDist' (X : Î© â†’ G) (Y : Î©' â†’ G) (W : Î©' â†’ T)
    (Î¼ : Measure Î© := by volume_tac) (Î¼' : Measure Î©' := by volume_tac) [IsFiniteMeasure Î¼'] : â„ :=
  dk[Kernel.const Unit (Î¼.map X) ; Measure.dirac () # condDistrib Y W Î¼' ; Î¼'.map W]

@[inherit_doc condRuzsaDist']
notation3:max "d[" X " ; " Î¼ " # " Y " | " W " ; " Î¼' "]" => condRuzsaDist' X Y W Î¼ Î¼'
@[inherit_doc condRuzsaDist']
notation3:max "d[" X " # " Y " | " W "]" => condRuzsaDist' X Y W volume volume

/-- Conditional Ruzsa distance equals Ruzsa distance of associated kernels. -/
lemma condRuzsaDist'_def {T : Type*} [MeasurableSpace T]
    (X : Î© â†’ G) (Y : Î©' â†’ G) (W : Î©' â†’ T) (Î¼ : Measure Î©) (Î¼' : Measure Î©')
    [IsFiniteMeasure Î¼'] :
    d[X ; Î¼ # Y | W ; Î¼'] =
      dk[Kernel.const Unit (Î¼.map X) ; Measure.dirac () # condDistrib Y W Î¼' ; Î¼'.map W] :=
  rfl

@[simp] lemma condRuzsaDist'_zero_right {T : Type*} [MeasurableSpace T]
    (X : Î© â†’ G) (Y : Î©' â†’ G) (W : Î©' â†’ T) (Î¼ : Measure Î©) :
    d[X ; Î¼ # Y | W ; 0] = 0 := by
  simp only [condRuzsaDist'_def, aemeasurable_zero_measure, not_true_eq_false, Measure.map_zero,
    Kernel.rdist_zero_right]

/-- Explicit formula for conditional Ruzsa distance `d[X ; Y | W]`. -/
lemma condRuzsaDist'_eq_sum {X : Î© â†’ G} {Y : Î©' â†’ G} {W : Î©' â†’ T} (hY : Measurable Y)
    (hW : Measurable W) (Î¼ : Measure Î©) (Î¼' : Measure Î©') [IsFiniteMeasure Î¼'] [FiniteRange W] :
    d[X ; Î¼ # Y | W ; Î¼']
      = âˆ‘ w in FiniteRange.toFinset W, (Î¼' (W â»Â¹' {w})).toReal * d[X ; Î¼ # Y ; (Î¼'[|W â† w])] := by
  have : Measure.prod (dirac ()) (Î¼'.map W) ((Finset.univ (Î± := Unit) Ã—Ë¢ FiniteRange.toFinset W :
    Finset (Unit Ã— T)) : Set (Unit Ã— T))á¶œ = 0 := by
    apply Measure.prod_of_full_measure_finset
    Â· simp
    rw [Measure.map_apply â€¹_â€º]
    convert measure_empty (Î¼ := Î¼)
    simp [â† FiniteRange.range]
    measurability
  rw [condRuzsaDist'_def, Kernel.rdist, integral_eq_setIntegral this, integral_finset _ _ IntegrableOn.finset]
  simp_rw [Measure.prod_apply_singleton, smul_eq_mul, Finset.sum_product]
  simp only [Finset.univ_unique, PUnit.default_eq_unit, MeasurableSpace.measurableSet_top,
    Measure.dirac_apply', Set.mem_singleton_iff, Set.indicator_of_mem, Pi.one_apply, one_mul,
    Finset.sum_singleton]
  simp_rw [Measure.map_apply hW (.singleton _)]
  congr with w
  by_cases hw : Î¼' (W â»Â¹' {w}) = 0
  Â· simp only [mul_eq_mul_left_iff]
    refine Or.inr ?_
    simp [ENNReal.toReal_eq_zero_iff, measure_ne_top Î¼', hw]
  congr 1
  rw [rdist_eq_rdistm, condDistrib_apply hY hW _ _ hw]
  congr

/-- Alternate formula for conditional Ruzsa distance `d[X ; Y | W]` when T is a Fintype. -/
lemma condRuzsaDist'_eq_sum' {X : Î© â†’ G} {Y : Î©' â†’ G} {W : Î©' â†’ T} (hY : Measurable Y)
    (hW : Measurable W) (Î¼ : Measure Î©) (Î¼' : Measure Î©') [IsFiniteMeasure Î¼'] [Fintype T] :
    d[X ; Î¼ # Y | W ; Î¼']
      = âˆ‘ w, (Î¼' (W â»Â¹' {w})).toReal * d[X ; Î¼ # Y ; (Î¼'[|W â† w])] := by
  rw [condRuzsaDist'_eq_sum hY hW Î¼ Î¼']
  apply Finset.sum_subset
  Â· simp
  intro w _ hw
  simp only [FiniteRange.mem_iff, not_exists] at hw
  have : Wâ»Â¹' {w} = âˆ… := by
    ext Ï‰; simp [hw Ï‰]
  simp [this]

open scoped ENNReal

lemma condRuzsaDist'_prod_eq_sum {X : Î© â†’ G} {Y : Î©' â†’ G} {W W' : Î©' â†’ T}
    (Î¼ : Measure Î©) (Î¼' : Measure Î©') (hY : Measurable Y) (hW' : Measurable W') (hW : Measurable W)
    [IsFiniteMeasure Î¼'] [FiniteRange W] [FiniteRange W']:
    d[X ; Î¼ # Y | âŸ¨W', WâŸ©; Î¼']
      = âˆ‘ w in FiniteRange.toFinset W, (Î¼' (W â»Â¹' {w})).toReal * d[X ; Î¼ # Y | W' ; (Î¼'[|W â† w])] := by
  have : d[X ; Î¼ # Y | âŸ¨W', WâŸ©; Î¼'] = âˆ‘ w in ((FiniteRange.toFinset W') Ã—Ë¢ FiniteRange.toFinset W),
    (Î¼' ((fun a => (W' a, W a)) â»Â¹' {w})).toReal * d[X ; Î¼ # Y ; Î¼'[|(fun a => (W' a, W a)) â»Â¹' {w}]] := by
    rw [condRuzsaDist'_eq_sum hY (hW'.prod_mk hW)]
    apply Finset.sum_subset
    Â· intro (t, t')
      simp only [FiniteRange.mem_iff, Prod.mk.injEq, Finset.mem_product, forall_exists_index,
        and_imp]
      exact fun Ï‰ h1 h2 â†¦ âŸ¨âŸ¨Ï‰, h1âŸ©, âŸ¨Ï‰, h2âŸ©âŸ©
    intro (t, t') _ ht
    simp only [FiniteRange.mem_iff, Prod.mk.injEq, not_exists, not_and] at ht
    have : (fun a => (W' a, W a)) â»Â¹' {(t, t')} = âˆ… := by
      ext Ï‰
      simpa using ht Ï‰
    simp [this]
  rw [this, Finset.sum_product_right]
  congr 1 with w
  rw [condRuzsaDist'_eq_sum hY hW', Finset.mul_sum]
  congr 1 with w'
  rw [â† mul_assoc]
  have A : (fun a â†¦ (W' a, W a)) â»Â¹' {(w', w)} = W' â»Â¹' {w'} âˆ© Wâ»Â¹' {w} := by ext; simp
  congr 1
  Â· simp only [A, ProbabilityTheory.cond, smul_apply, restrict_apply (hW' (.singleton w')),
      smul_eq_mul, ENNReal.toReal_mul]
    rcases eq_bot_or_bot_lt (Î¼' (W â»Â¹' {w})) with hw|hw
    Â· have : Î¼' (W' â»Â¹' {w'} âˆ© W â»Â¹' {w}) = 0 :=
        le_antisymm (le_trans (measure_mono Set.inter_subset_right) hw.le) bot_le
      simp [hw, this]
    Â· rw [â† mul_assoc, â† ENNReal.toReal_mul, ENNReal.mul_inv_cancel, ENNReal.one_toReal, one_mul]
      exacts [hw.ne', by finiteness]
  Â· congr 1
    rw [A, cond_cond_eq_cond_inter' (hW (.singleton _)) (hW' (.singleton _)), Set.inter_comm]
    finiteness

/-- Version of `condRuzsaDist'_prod_eq_sum` when `W` has finite codomain. -/
lemma condRuzsaDist'_prod_eq_sum' {X : Î© â†’ G} {Y : Î©' â†’ G} {W W' : Î©' â†’ T}
    (Î¼ : Measure Î©) (Î¼' : Measure Î©') (hY : Measurable Y) (hW' : Measurable W') (hW : Measurable W)
    [IsFiniteMeasure Î¼'] [Fintype T]:
    d[X ; Î¼ # Y | âŸ¨W', WâŸ©; Î¼']
      = âˆ‘ w, (Î¼' (W â»Â¹' {w})).toReal * d[X ; Î¼ # Y | W' ; (Î¼'[|W â† w])] := by
  rw [condRuzsaDist'_prod_eq_sum Î¼ Î¼' hY hW' hW]
  apply Finset.sum_subset
  Â· simp
  intro w _ hw
  simp only [FiniteRange.mem_iff, not_exists] at hw
  have : Wâ»Â¹' {w} = âˆ… := by
    ext Ï‰; simp [hw Ï‰]
  simp [this]

/-- Explicit formula for conditional Ruzsa distance `d[X ; Y | W]`, in integral form. -/
lemma condRuzsaDist'_eq_integral (X : Î© â†’ G) {Y : Î©' â†’ G} {W : Î©' â†’ T}
    (hY : Measurable Y) (hW : Measurable W)
    (Î¼ : Measure Î©) (Î¼' : Measure Î©') [IsFiniteMeasure Î¼'] [FiniteRange W] :
    d[X ; Î¼ # Y | W ; Î¼']
      = (Î¼'.map W)[fun w â†¦ d[X ; Î¼ # Y ; (Î¼'[|W â† w])]] := by
  rw [condRuzsaDist'_eq_sum hY hW]
  simp_rw [â† smul_eq_mul]
  have : (Î¼'.map W) (FiniteRange.toFinset W : Set T)á¶œ = 0 := by
    rw [Measure.map_apply â€¹_â€º]
    convert measure_empty (Î¼ := Î¼)
    simp [â† FiniteRange.range]
    measurability
  rw [integral_eq_setIntegral this, integral_finset _ _ IntegrableOn.finset]
  simp [Measure.map_apply hW (MeasurableSet.singleton _)]

section

variable [Countable T]

/-- Conditioning by a constant does not affect Ruzsa distance. -/
lemma condRuzsaDist_of_const {X : Î© â†’ G} (hX : Measurable X)
    (Y : Î©' â†’ G) (W : Î©' â†’ T) (c : S)
    [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î¼'] [FiniteRange W] :
    d[X|(fun _ â†¦ c) ; Î¼ # Y | W ; Î¼'] = d[X ; Î¼ # Y | W ; Î¼'] := by
  rw [condRuzsaDist_def, condRuzsaDist'_def, Measure.map_const,measure_univ,one_smul, Kernel.rdist,
    Kernel.rdist, integral_prod, integral_dirac, integral_prod,integral_dirac]
  dsimp; congr; ext x; congr
  rw [condDistrib_apply hX measurable_const]
  Â· simp
  Â· simp
  Â· exact integrable_of_finiteSupport _
  Â· exact integrable_of_finiteSupport _

/-- If `$(X,Z)$` and `$(Y,W)$` are independent, then
`d[X | Z ; Y | W] = H[X'- Y' | Z', W'] - H[X'|Z']/2 - H[Y'|W']/2`. -/
lemma condRuzsaDist_of_indep
    {X : Î© â†’ G} {Z : Î© â†’ S} {Y : Î© â†’ G} {W : Î© â†’ T}
    (hX : Measurable X) (hZ : Measurable Z) (hY : Measurable Y) (hW : Measurable W)
    (Î¼ : Measure Î©) [IsProbabilityMeasure Î¼]
    (h : IndepFun (âŸ¨X, ZâŸ©) (âŸ¨Y, WâŸ©) Î¼) [FiniteRange Z] [FiniteRange W] :
    d[X | Z ; Î¼ # Y | W ; Î¼] = H[X - Y | âŸ¨Z, WâŸ© ; Î¼] - H[X | Z ; Î¼]/2 - H[Y | W ; Î¼]/2 := by
  have : IsProbabilityMeasure (Î¼.map Z) := isProbabilityMeasure_map hZ.aemeasurable
  have : IsProbabilityMeasure (Î¼.map W) := isProbabilityMeasure_map hW.aemeasurable
  rw [condRuzsaDist_def, Kernel.rdist_eq', condEntropy_eq_kernel_entropy _ (hZ.prod_mk hW),
    condEntropy_eq_kernel_entropy hX hZ, condEntropy_eq_kernel_entropy hY hW]
  swap; Â· exact hX.sub hY
  congr 2
  have hZW : IndepFun Z W Î¼ := h.comp measurable_snd measurable_snd
  have hZW_map : Î¼.map (âŸ¨Z, WâŸ©) = (Î¼.map Z).prod (Î¼.map W) :=
    (indepFun_iff_map_prod_eq_prod_map_map hZ.aemeasurable hW.aemeasurable).mp hZW
  rw [â† hZW_map]
  refine Kernel.entropy_congr ?_
  have : Kernel.map (condDistrib (âŸ¨X, YâŸ©) (âŸ¨Z, WâŸ©) Î¼) (fun x â†¦ x.1 - x.2)
      =áµ[Î¼.map (âŸ¨Z, WâŸ©)] condDistrib (X - Y) (âŸ¨Z, WâŸ©) Î¼ :=
    (condDistrib_comp (hX.prod_mk hY) (hZ.prod_mk hW) _ _).symm
  refine (this.symm.trans ?_).symm
  suffices Kernel.prodMkRight T (condDistrib X Z Î¼)
        Ã—â‚– Kernel.prodMkLeft S (condDistrib Y W Î¼)
      =áµ[Î¼.map (âŸ¨Z, WâŸ©)] condDistrib (âŸ¨X, YâŸ©) (âŸ¨Z, WâŸ©) Î¼ by
    filter_upwards [this] with x hx
    rw [Kernel.map_apply _ (by fun_prop), Kernel.map_apply _ (by fun_prop), hx]
  exact (condDistrib_eq_prod_of_indepFun hX hZ hY hW Î¼ h).symm

/-- Formula for conditional Ruzsa distance for independent sets of variables. -/
lemma condRuzsaDist'_of_indep {X : Î© â†’ G} {Y : Î© â†’ G} {W : Î© â†’ T}
    (hX : Measurable X) (hY : Measurable Y) (hW : Measurable W)
    (Î¼ : Measure Î©) [IsProbabilityMeasure Î¼]
    (h : IndepFun X (âŸ¨Y, WâŸ©) Î¼) [FiniteRange W] :
    d[X ; Î¼ # Y | W ; Î¼] = H[X - Y | W ; Î¼] - H[X ; Î¼]/2 - H[Y | W ; Î¼]/2 := by
  have : IsProbabilityMeasure (Î¼.map W) := isProbabilityMeasure_map hW.aemeasurable
  rw [condRuzsaDist'_def, Kernel.rdist_eq', condEntropy_eq_kernel_entropy _ hW,
    condEntropy_eq_kernel_entropy hY hW, entropy_eq_kernel_entropy]
  rotate_left
  Â· exact hX.sub hY
  congr 2
  let Z : Î© â†’ Unit := fun _ â†¦ ()
  rw [â† condDistrib_unit_right hX Î¼]
  have h' : IndepFun (âŸ¨X,ZâŸ©) (âŸ¨Y, WâŸ©) Î¼ := by
    rw [indepFun_iff_measure_inter_preimage_eq_mul]
    intro s t hs ht
    have : âŸ¨X, ZâŸ© â»Â¹' s = X â»Â¹' ((fun c â†¦ (c, ())) â»Â¹' s) := by ext1 y; simp
    rw [this]
    rw [indepFun_iff_measure_inter_preimage_eq_mul] at h
    exact h _ _ (measurable_prod_mk_right hs) ht
  have h_indep := condDistrib_eq_prod_of_indepFun hX measurable_const hY hW _ h'
  have h_meas_eq : Î¼.map (âŸ¨Z, WâŸ©) = (Measure.dirac ()).prod (Î¼.map W) := by
    ext s hs
    rw [Measure.map_apply (measurable_const.prod_mk hW) hs, Measure.prod_apply hs, lintegral_dirac,
      Measure.map_apply hW (measurable_prod_mk_left hs)]
    congr
  rw [â† h_meas_eq]
  have : Kernel.map (Kernel.prodMkRight T (condDistrib X Z Î¼)
        Ã—â‚– Kernel.prodMkLeft Unit (condDistrib Y W Î¼)) (fun x â†¦ x.1 - x.2)
      =áµ[Î¼.map (âŸ¨Z, WâŸ©)] Kernel.map (condDistrib (âŸ¨X, YâŸ©) (âŸ¨Z, WâŸ©) Î¼) (fun x â†¦ x.1 - x.2) := by
    filter_upwards [h_indep] with y hy
    conv_rhs => rw [Kernel.map_apply _ (by fun_prop), hy]
    rw [â† Kernel.mapOfMeasurable_eq_map _ (by fun_prop)]
    rfl
  rw [Kernel.entropy_congr this]
  have : Kernel.map (condDistrib (âŸ¨X, YâŸ©) (âŸ¨Z, WâŸ©) Î¼) (fun x â†¦ x.1 - x.2)
      =áµ[Î¼.map (âŸ¨Z, WâŸ©)] condDistrib (X - Y) (âŸ¨Z, WâŸ©) Î¼ :=
    (condDistrib_comp (hX.prod_mk hY) (measurable_const.prod_mk hW) _ _).symm
  rw [Kernel.entropy_congr this]
  have h_meas : Î¼.map (âŸ¨Z, WâŸ©) = (Î¼.map W).map (Prod.mk ()) := by
    ext s hs
    rw [Measure.map_apply measurable_prod_mk_left hs, h_meas_eq, Measure.prod_apply hs,
      lintegral_dirac]
  have h_ker : condDistrib (X - Y) (âŸ¨Z, WâŸ©) Î¼
      =áµ[Î¼.map (âŸ¨Z, WâŸ©)] Kernel.prodMkLeft Unit (condDistrib (X - Y) W Î¼) := by
    rw [Filter.EventuallyEq, ae_iff_of_countable]
    intro x hx
    rw [Measure.map_apply (measurable_const.prod_mk hW) (.singleton _)] at hx
    ext s hs
    have h_preimage_eq : (fun a â†¦ (PUnit.unit, W a)) â»Â¹' {x} = W â»Â¹' {x.2} := by
      conv_lhs => rw [â† Prod.eta x, â† Set.singleton_prod_singleton, Set.mk_preimage_prod]
      ext1 y
      simp
    rw [Kernel.prodMkLeft_apply, condDistrib_apply' _ (measurable_const.prod_mk hW) _ _ hx hs,
      condDistrib_apply' _ hW _ _ _ hs]
    rotate_left
    Â· exact hX.sub hY
    Â· convert hx
      exact h_preimage_eq.symm
    Â· exact hX.sub hY
    congr
  rw [Kernel.entropy_congr h_ker, h_meas, Kernel.entropy_prodMkLeft_unit]

end

omit [Countable S] in
/-- The conditional Ruzsa distance is unchanged if the sets of random variables are replaced with
copies. -/
lemma condRuzsaDist_of_copy {X : Î© â†’ G} (hX : Measurable X) {Z : Î© â†’ S} (hZ : Measurable Z)
    {Y : Î©' â†’ G} (hY : Measurable Y) {W : Î©' â†’ T} (hW : Measurable W)
    {X' : Î©'' â†’ G} (hX' : Measurable X') {Z' : Î©'' â†’ S} (hZ' : Measurable Z')
    {Y' : Î©''' â†’ G} (hY' : Measurable Y') {W' : Î©''' â†’ T} (hW' : Measurable W')
    [IsFiniteMeasure Î¼] [IsFiniteMeasure Î¼'] [IsFiniteMeasure Î¼''] [IsFiniteMeasure Î¼''']
    (h1 : IdentDistrib (âŸ¨X, ZâŸ©) (âŸ¨X', Z'âŸ©) Î¼ Î¼'') (h2 : IdentDistrib (âŸ¨Y, WâŸ©) (âŸ¨Y', W'âŸ©) Î¼' Î¼''')
    [FiniteRange Z] [FiniteRange W] [FiniteRange Z'] [FiniteRange W'] :
    d[X | Z ; Î¼ # Y | W ; Î¼'] = d[X' | Z' ; Î¼'' # Y' | W' ; Î¼'''] := by
  classical
  set A := (FiniteRange.toFinset Z) âˆª (FiniteRange.toFinset Z')
  set B := (FiniteRange.toFinset W) âˆª (FiniteRange.toFinset W')
  have hfull : Measure.prod (Î¼.map Z) (Î¼'.map W) ((A Ã—Ë¢ B : Finset (S Ã— T)): Set (S Ã— T))á¶œ = 0 := by
    simp only [A, B]
    apply Measure.prod_of_full_measure_finset
    all_goals {
      rw [Measure.map_apply â€¹_â€º]
      convert measure_empty (Î¼ := Î¼)
      simp [â† FiniteRange.range]
      measurability
    }
  have hfull' : Measure.prod (Î¼''.map Z') (Î¼'''.map W')
    ((A Ã—Ë¢ B : Finset (S Ã— T)): Set (S Ã— T))á¶œ = 0 := by
    simp only [A, B]
    apply Measure.prod_of_full_measure_finset
    all_goals {
      rw [Measure.map_apply â€¹_â€º]
      convert measure_empty (Î¼ := Î¼)
      simp [â† FiniteRange.range]
      measurability
    }
  rw [condRuzsaDist_def, condRuzsaDist_def, Kernel.rdist, Kernel.rdist,
    integral_eq_setIntegral hfull, integral_eq_setIntegral hfull', integral_finset _ _ IntegrableOn.finset,
    integral_finset _ _ IntegrableOn.finset]
  have hZZ' : Î¼.map Z = Î¼''.map Z' := (h1.comp measurable_snd).map_eq
  have hWW' : Î¼'.map W = Î¼'''.map W' := (h2.comp measurable_snd).map_eq
  simp_rw [Measure.prod_apply_singleton, ENNReal.toReal_mul, â† hZZ', â† hWW',
    Measure.map_apply hZ (.singleton _),
    Measure.map_apply hW (.singleton _)]
  congr with x
  by_cases hz : Î¼ (Z â»Â¹' {x.1}) = 0
  Â· simp only [smul_eq_mul, mul_eq_mul_left_iff, mul_eq_zero]
    refine Or.inr (Or.inl ?_)
    simp [ENNReal.toReal_eq_zero_iff, measure_ne_top, hz]
  by_cases hw : Î¼' (W â»Â¹' {x.2}) = 0
  Â· simp only [smul_eq_mul, mul_eq_mul_left_iff, mul_eq_zero]
    refine Or.inr (Or.inr ?_)
    simp [ENNReal.toReal_eq_zero_iff, measure_ne_top, hw]
  congr 2
  Â· have hZZ'x : Î¼ (Z â»Â¹' {x.1}) = Î¼'' (Z' â»Â¹' {x.1}) := by
      have : Î¼.map Z {x.1} = Î¼''.map Z' {x.1} := by rw [hZZ']
      rwa [Measure.map_apply hZ (.singleton _),
        Measure.map_apply hZ' (.singleton _)] at this
    ext s hs
    rw [condDistrib_apply' hX hZ _ _ hz hs, condDistrib_apply' hX' hZ' _ _ _ hs]
    swap; Â· rwa [hZZ'x] at hz
    congr
    have : Î¼.map (âŸ¨X, ZâŸ©) (s Ã—Ë¢ {x.1}) = Î¼''.map (âŸ¨X', Z'âŸ©) (s Ã—Ë¢ {x.1}) := by rw [h1.map_eq]
    rwa [Measure.map_apply (hX.prod_mk hZ) (hs.prod (.singleton _)),
      Measure.map_apply (hX'.prod_mk hZ') (hs.prod (.singleton _)),
      Set.mk_preimage_prod, Set.mk_preimage_prod, Set.inter_comm,
      Set.inter_comm ((fun a â†¦ X' a) â»Â¹' s)] at this
  Â· have hWW'x : Î¼' (W â»Â¹' {x.2}) = Î¼''' (W' â»Â¹' {x.2}) := by
      have : Î¼'.map W {x.2} = Î¼'''.map W' {x.2} := by rw [hWW']
      rwa [Measure.map_apply hW (.singleton _),
        Measure.map_apply hW' (.singleton _)] at this
    ext s hs
    rw [condDistrib_apply' hY hW _ _ hw hs, condDistrib_apply' hY' hW' _ _ _ hs]
    swap; Â· rwa [hWW'x] at hw
    congr
    have : Î¼'.map (âŸ¨Y, WâŸ©) (s Ã—Ë¢ {x.2}) = Î¼'''.map (âŸ¨Y', W'âŸ©) (s Ã—Ë¢ {x.2}) := by rw [h2.map_eq]
    rwa [Measure.map_apply (hY.prod_mk hW) (hs.prod (.singleton _)),
      Measure.map_apply (hY'.prod_mk hW') (hs.prod (.singleton _)),
      Set.mk_preimage_prod, Set.mk_preimage_prod, Set.inter_comm,
      Set.inter_comm ((fun a â†¦ Y' a) â»Â¹' s)] at this

lemma condRuzsaDist'_of_copy (X : Î© â†’ G) {Y : Î©' â†’ G} (hY : Measurable Y)
    {W : Î©' â†’ T} (hW : Measurable W)
    (X' : Î©'' â†’ G) {Y' : Î©''' â†’ G} (hY' : Measurable Y') {W' : Î©''' â†’ T} (hW' : Measurable W')
    [IsFiniteMeasure Î¼'] [IsFiniteMeasure Î¼''']
    (h1 : IdentDistrib X X' Î¼ Î¼'') (h2 : IdentDistrib (âŸ¨Y, WâŸ©) (âŸ¨Y', W'âŸ©) Î¼' Î¼''')
    [FiniteRange W] [FiniteRange W'] :
    d[X ; Î¼ # Y | W ; Î¼'] = d[X' ; Î¼'' # Y' | W' ; Î¼'''] := by
  classical
  set A := (FiniteRange.toFinset W) âˆª (FiniteRange.toFinset W')
  have hfull : Measure.prod (dirac ()) (Î¼'.map W)
      ((Finset.univ (Î± := Unit) Ã—Ë¢ A : Finset (Unit Ã— T)) : Set (Unit Ã— T))á¶œ = 0 := by
    apply Measure.prod_of_full_measure_finset
    Â· simp
    simp only [A]
    rw [Measure.map_apply â€¹_â€º]
    convert measure_empty (Î¼ := Î¼)
    simp [â† FiniteRange.range]
    measurability
  have hfull' : Measure.prod (dirac ()) (Î¼'''.map W')
      ((Finset.univ (Î± := Unit) Ã—Ë¢ A : Finset (Unit Ã— T)) : Set (Unit Ã— T))á¶œ = 0 := by
    apply Measure.prod_of_full_measure_finset
    Â· simp
    simp only [A]
    rw [Measure.map_apply â€¹_â€º]
    convert measure_empty (Î¼ := Î¼)
    simp [â† FiniteRange.range]
    measurability
  rw [condRuzsaDist'_def, condRuzsaDist'_def, Kernel.rdist, Kernel.rdist,
    integral_eq_setIntegral hfull, integral_eq_setIntegral hfull', integral_finset _ _ IntegrableOn.finset,
    integral_finset _ _ IntegrableOn.finset]
  have hWW' : Î¼'.map W = Î¼'''.map W' := (h2.comp measurable_snd).map_eq
  simp_rw [Measure.prod_apply_singleton, ENNReal.toReal_mul, â† hWW',
    Measure.map_apply hW (.singleton _)]
  congr with x
  by_cases hw : Î¼' (W â»Â¹' {x.2}) = 0
  Â· simp only [smul_eq_mul, mul_eq_mul_left_iff, mul_eq_zero]
    refine Or.inr (Or.inr ?_)
    simp [ENNReal.toReal_eq_zero_iff, measure_ne_top, hw]
  congr 2
  Â· rw [Kernel.const_apply, Kernel.const_apply, h1.map_eq]
  Â· have hWW'x : Î¼' (W â»Â¹' {x.2}) = Î¼''' (W' â»Â¹' {x.2}) := by
      have : Î¼'.map W {x.2} = Î¼'''.map W' {x.2} := by rw [hWW']
      rwa [Measure.map_apply hW (.singleton _),
        Measure.map_apply hW' (.singleton _)] at this
    ext s hs
    rw [condDistrib_apply' hY hW _ _ hw hs, condDistrib_apply' hY' hW' _ _ _ hs]
    swap; Â· rwa [hWW'x] at hw
    congr
    have : Î¼'.map (âŸ¨Y, WâŸ©) (s Ã—Ë¢ {x.2}) = Î¼'''.map (âŸ¨Y', W'âŸ©) (s Ã—Ë¢ {x.2}) := by rw [h2.map_eq]
    rwa [Measure.map_apply (hY.prod_mk hW) (hs.prod (.singleton _)),
      Measure.map_apply (hY'.prod_mk hW') (hs.prod (.singleton _)),
      Set.mk_preimage_prod, Set.mk_preimage_prod, Set.inter_comm,
      Set.inter_comm ((fun a â†¦ Y' a) â»Â¹' s)] at this

lemma condRuszaDist_prod_eq_of_indepFun {Î¼ : Measure Î©} {Î¼' : Measure Î©'} {X : Î© â†’ G} {Y : Î©' â†’ G}
    {W W' : Î©' â†’ T} (hX : Measurable X) (hY : Measurable Y) (hW : Measurable W)
    (hW' : Measurable W') (h : IndepFun (âŸ¨Y, WâŸ©) W' Î¼')
    [IsProbabilityMeasure Î¼'] [Fintype T] :
    d[X ; Î¼ # Y | âŸ¨W, W'âŸ© ; Î¼'] = d[X ; Î¼ # Y | W ; Î¼'] := by
  rw [condRuzsaDist'_prod_eq_sum' _ _ hY hW hW']
  have : d[X ; Î¼ # Y | W ; Î¼'] = âˆ‘ z, (Î¼' (W' â»Â¹' {z})).toReal * d[X ; Î¼ # Y | W ; Î¼'] := by
    rw [â† Finset.sum_mul, sum_measure_preimage_singleton' Î¼' hW', one_mul]
  rw [this]
  congr with w
  rcases eq_or_ne (Î¼' (W' â»Â¹' {w})) 0 with hw|hw
  Â· simp [hw]
  congr 1
  exact condRuzsaDist'_of_copy _ hY hW _ hY hW (IdentDistrib.refl hX.aemeasurable)
    (h.identDistrib_cond (MeasurableSet.singleton w) (hY.prod_mk hW) hW' hw).symm

variable (Î¼ Î¼') in
lemma condRuzsaDist_comp_right {T' : Type*} [Fintype T] [Fintype T'] [MeasurableSpace T']
    [MeasurableSingletonClass T'] [IsFiniteMeasure Î¼']
    (X : Î© â†’ G) (Y : Î©' â†’ G) (W : Î©' â†’ T) (e : T â†’ T')
    (hY : Measurable Y) (hW : Measurable W) (he : Measurable e)
    (h'e : Injective e) :
    d[X ; Î¼ # Y | e âˆ˜ W ; Î¼'] = d[X ; Î¼ # Y | W ; Î¼'] := by
  rw [condRuzsaDist'_eq_sum' hY (he.comp hW), condRuzsaDist'_eq_sum' hY hW]
  simp [Set.preimage_comp]
  have A i : e â»Â¹' {e i} = {i} := by ext x; simp [h'e.eq_iff]
  symm
  refine Fintype.sum_of_injective e h'e _ _ (fun i hi â†¦ ?_) (by simp [A])
  suffices e â»Â¹' {i} = âˆ… by simp [this]
  simpa [Set.eq_empty_iff_forall_not_mem] using hi

lemma condRuzsaDist_of_inj_map {G' : Type*} [Countable G'] [AddCommGroup G']
  [MeasurableSpace G'] [MeasurableSingletonClass G'] [IsProbabilityMeasure Î¼]
  (Y : Fin 4 â†’ Î© â†’ G) (h_indep : IndepFun (âŸ¨Y 0, Y 2âŸ©) (âŸ¨Y 1, Y 3âŸ©) Î¼)
  (h_meas : âˆ€ i, Measurable (Y i)) (Ï€ : G Ã— G â†’+ G')
  (hÏ€ : âˆ€ (h : G), Injective (fun g â†¦ Ï€ (g, h)))
  [FiniteRange (Y 2)] [FiniteRange (Y 3)] :
    d[Ï€ âˆ˜ âŸ¨Y 0, Y 2âŸ© | Y 2 ; Î¼ # Ï€ âˆ˜ âŸ¨Y 1, Y 3âŸ© | Y 3 ; Î¼] = d[Y 0 | Y 2 ; Î¼ # Y 1 | Y 3 ; Î¼] := by
  let f (h : G) (g : G) : G' := Ï€ (g, h)
  let f' : G Ã— G â†’ G â†’ G' := fun (h1, h2) â†¦ fun g â†¦ Ï€ (g, h1 - h2)
  have hf' (t : G Ã— G) : Injective (f' t) := fun _ _ h â†¦ hÏ€ _ h
  let f'' : G Ã— G â†’ G' Ã— G := fun (g, h) â†¦ (Ï€ (g, h), h)
  have hf'' : Measurable f'' := .of_discrete
  have hm1 : Measurable (Y 0 - Y 1) := (h_meas 0).sub (h_meas 1)
  have hm2 : Measurable (âŸ¨Y 2, Y 3âŸ©) := (h_meas 2).prod_mk (h_meas 3)
  rw [condRuzsaDist_of_indep (h_meas 0) (h_meas 2) (h_meas 1) (h_meas 3) Î¼ h_indep,
    condRuzsaDist_of_indep (by fun_prop) (by fun_prop) (by fun_prop) (by fun_prop) Î¼
    (by exact h_indep.comp hf'' hf''),
    â† condEntropy_of_injective Î¼ hm1 hm2 f' hf', â† map_comp_sub Ï€,
    â† condEntropy_of_injective Î¼ (h_meas 0) (h_meas 2) f hÏ€,
    â† condEntropy_of_injective Î¼ (h_meas 1) (h_meas 3) f hÏ€]
  rfl

lemma condRuzsaDist'_of_inj_map [IsProbabilityMeasure Î¼] [Module (ZMod 2) G]
  {X B C : Î© â†’ G}
    (hX : Measurable X) (hB : Measurable B) (hC : Measurable C)
    (h_indep : IndepFun X (âŸ¨B, CâŸ©) Î¼) [FiniteRange X] [FiniteRange B] [FiniteRange C] :
    d[X ; Î¼ # B | B + C ; Î¼] = d[X ; Î¼ # C | B + C ; Î¼] := by
  let Ï€ : G Ã— G â†’+ G :=
  { toFun := fun x â†¦ x.2 - x.1
    map_zero' := by simp
    map_add' := fun a b â†¦ by simp only [Prod.snd_add, Prod.fst_add, ZModModule.sub_eq_add]; abel }
  let Y : Fin 4 â†’ Î© â†’ G := ![-X, C, fun _ â†¦ 0, B + C]
  have _ : FiniteRange (Y 0) := by simp [Y]; infer_instance
  have _ : FiniteRange (Y 1) := by simp [Y]; infer_instance
  have _ : FiniteRange (Y 2) := by simp [Y]; infer_instance
  have _ : FiniteRange (Y 3) := by simp [Y]; infer_instance
  have hY_meas i : Measurable (Y i) := by
    fin_cases i; exacts [hX.neg, hC, measurable_const, hB.add hC]
  calc d[X ; Î¼ # B | B + C ; Î¼]
    = d[X | fun _ : Î© â†¦ (0 : G) ; Î¼ # B | B + C ; Î¼] := by rw [condRuzsaDist_of_const hX]
  _ = d[Ï€ âˆ˜ âŸ¨-X, fun _ : Î© â†¦ (0 : G)âŸ© | fun _ : Î© â†¦ (0 : G) ; Î¼ # Ï€ âˆ˜ âŸ¨C, B + CâŸ© | B + C ; Î¼] := by
    congr
    Â· ext1 Ï‰; simp [Ï€]
    Â· ext1 Ï‰
      simp only [AddMonoidHom.coe_mk, ZeroHom.coe_mk, comp_apply, Pi.add_apply, Ï€]
      abel
  _ = d[Ï€ âˆ˜ âŸ¨Y 0, Y 2âŸ© | Y 2 ; Î¼ # Ï€ âˆ˜ âŸ¨Y 1, Y 3âŸ© | Y 3 ; Î¼] := by congr
  _ = d[-X | fun _ : Î© â†¦ (0 : G) ; Î¼ # C | B + C ; Î¼] := by
    rw [condRuzsaDist_of_inj_map _ _ hY_meas Ï€ (fun _ â†¦ sub_right_injective)]
    Â· congr
    Â· have h1 : (âŸ¨Y 0, Y 2âŸ©) = (fun x â†¦ (-x, 0)) âˆ˜ X := by ext1 Ï‰; simp [Y]
      have h2 : (âŸ¨Y 1, Y 3âŸ©) = (fun p â†¦ (p.2, p.1 + p.2)) âˆ˜ (âŸ¨B, CâŸ©) := by
        ext1 Ï‰;
        simp only [ZModModule.neg_eq_self, Matrix.cons_val_one, Matrix.head_cons,
          comp_apply, Prod.mk.injEq, Matrix.cons_val', Pi.add_apply, Matrix.empty_val',
          Matrix.cons_val_fin_one, true_and, Y]
        congr
      rw [h1, h2]
      refine h_indep.comp ?_ ?_
      Â· exact measurable_neg.prod_mk measurable_const
      Â· exact measurable_snd.prod_mk (measurable_fst.add measurable_snd)
  _ = d[-X ; Î¼ # C | B + C ; Î¼] := by rw [condRuzsaDist_of_const]; exact hX.neg
  _ = d[X ; Î¼ # C | B + C ; Î¼] := by simp_rw [ZModModule.neg_eq_self]

lemma condRuzsaDist'_of_inj_map' [Module (ZMod 2) G] [IsProbabilityMeasure Î¼]
    [IsProbabilityMeasure Î¼''] {A : Î©'' â†’ G} {B C : Î© â†’ G} (hA : Measurable A) (hB : Measurable B)
    (hC : Measurable C) [FiniteRange A] [FiniteRange B] [FiniteRange C] :
    d[A ; Î¼'' # B | B + C ; Î¼] = d[A ; Î¼'' # C | B + C ; Î¼] := by
  -- we want to apply `condRuzsaDist'_of_inj_map'`, but for that all variables need to
  -- be in the same probability space
  let Î©' := Î©'' Ã— Î©
  set Xâ‚‚' : Î©' â†’ G := A âˆ˜ Prod.fst with hXâ‚‚'_def
  have hXâ‚‚' : Measurable Xâ‚‚' := hA.comp measurable_fst
  let B' : Î©' â†’ G := B âˆ˜ Prod.snd
  have hB' : Measurable B' := hB.comp measurable_snd
  let C' : Î©' â†’ G := C âˆ˜ Prod.snd
  have hC' : Measurable C' := hC.comp measurable_snd
  let Î¼' : Measure Î©' := Measure.prod Î¼'' Î¼
  haveI : IsProbabilityMeasure Î¼' := by infer_instance
  -- h1 and h2 should be applications of a new lemma?
  have h1 : d[A ; Î¼'' # B | B + C ; Î¼] = d[Xâ‚‚' ; Î¼' # B' | B' + C' ; Î¼'] := by
    refine condRuzsaDist'_of_copy A hB (by fun_prop) Xâ‚‚' hB' (by fun_prop) ?_ ?_
    Â· constructor
      Â· exact hA.aemeasurable
      Â· exact hXâ‚‚'.aemeasurable
      Â· rw [â† Measure.map_map hA measurable_fst]
        simp [Î¼']
    Â· constructor
      Â· exact (hB.prod_mk (hB.add hC)).aemeasurable
      Â· exact (hB'.prod_mk (hB'.add hC')).aemeasurable
      Â· have : âŸ¨B', B' + C'âŸ© = (âŸ¨B, B + CâŸ©) âˆ˜ Prod.snd := by ext1 _; simp [C', B']
        rw [this, â† Measure.map_map _ measurable_snd]
        Â· simp only [Measure.map_snd_prod, measure_univ, one_smul, Î¼']
        Â· exact hB.prod_mk (hB.add hC)
  have h2 : d[A ; Î¼'' # C | B + C ; Î¼] = d[Xâ‚‚' ; Î¼' # C' | B' + C' ; Î¼'] := by
    apply condRuzsaDist'_of_copy _ hC (by fun_prop) Xâ‚‚' hC' (by fun_prop) ?_ ?_
    Â· constructor
      Â· exact hA.aemeasurable
      Â· exact hXâ‚‚'.aemeasurable
      Â· rw [â† Measure.map_map hA measurable_fst]
        simp [Î¼']
    Â· constructor
      Â· exact (hC.prod_mk (hB.add hC)).aemeasurable
      Â· exact (hC'.prod_mk (hB'.add hC')).aemeasurable
      Â· have : âŸ¨C', B' + C'âŸ© = (âŸ¨C, B + CâŸ©) âˆ˜ Prod.snd := by ext1 _; simp [B', C']
        rw [this, â† Measure.map_map _ measurable_snd]
        Â· simp only [Measure.map_snd_prod, measure_univ, one_smul, Î¼']
        Â· exact hC.prod_mk (hB.add hC)
  rw [h1, h2, condRuzsaDist'_of_inj_map hXâ‚‚' hB' hC']
  rw [indepFun_iff_map_prod_eq_prod_map_map hXâ‚‚'.aemeasurable (hB'.prod_mk hC').aemeasurable]
  have h_prod : (fun Ï‰ â†¦ (Xâ‚‚' Ï‰, prod B' C' Ï‰)) = Prod.map A (âŸ¨B, CâŸ©) := by
    ext1
    simp only [comp_apply, Xâ‚‚', B', C']
    rfl

  have h_comp_snd : (fun a â†¦ (B' a, C' a)) = (âŸ¨B, CâŸ©) âˆ˜ Prod.snd := by ext1; simp [B', C']
  rw [h_prod, h_comp_snd, hXâ‚‚'_def, â† Measure.map_map _ measurable_snd,
    â† Measure.map_map _ measurable_fst, Measure.map_prod_map]
  rotate_left
  Â· exact hA
  Â· exact hB.prod_mk hC
  Â· exact hA
  Â· exact hB.prod_mk hC
  simp [Î¼']

/-- The **Kaimanovich-Vershik inequality**. `H[X + Y + Z] - H[X + Y] â‰¤ H[Y + Z] - H[Y]`. -/
lemma kaimanovich_vershik {X Y Z : Î© â†’ G} (h : iIndepFun (fun _ â†¦ hG) ![X, Y, Z] Î¼)
    (hX : Measurable X) (hY : Measurable Y) (hZ : Measurable Z)
    [FiniteRange X] [FiniteRange Z] [FiniteRange Y] :
    H[X + Y + Z ; Î¼] - H[X + Y ; Î¼] â‰¤ H[Y + Z ; Î¼] - H[Y ; Î¼] := by
  have : IsProbabilityMeasure Î¼ := h.isProbabilityMeasure
  suffices (H[X ; Î¼] + H[Y ; Î¼] + H[Z ; Î¼]) + H[X + Y + Z ; Î¼]
    â‰¤ (H[X ; Î¼] + H[Y + Z ; Î¼]) + (H[Z ; Î¼] + H[X + Y ; Î¼]) by linarith
  have : âˆ€ (i : Fin 3), Measurable (![X, Y, Z] i) := fun i â†¦ by fin_cases i <;> assumption
  convert entropy_triple_add_entropy_le Î¼ hX hZ (show Measurable (X + (Y + Z)) by fun_prop)
    using 2
  Â· calc
      H[X ; Î¼] + H[Y ; Î¼] + H[Z ; Î¼] = H[âŸ¨X, YâŸ© ; Î¼] + H[Z ; Î¼] := by
        rw [IndepFun.entropy_pair_eq_add hX hY]
        convert h.indepFun (show 0 â‰  1 by decide)
      _ = H[âŸ¨âŸ¨X, YâŸ©, ZâŸ© ; Î¼] := by
        rw [IndepFun.entropy_pair_eq_add (hX.prod_mk hY) hZ]
        exact h.indepFun_prod_mk this 0 1 2 (by decide) (by decide)
      _ = H[âŸ¨X, âŸ¨Z , X + (Y + Z)âŸ©âŸ© ; Î¼] := by
        apply entropy_of_comp_eq_of_comp Î¼ (by fun_prop) (by fun_prop)
          (fun ((x, y), z) â†¦ (x, z, x + y + z)) (fun (a, b, c) â†¦ ((a, c - a - b), b))
        all_goals { funext Ï‰; dsimp [prod]; ext <;> dsimp; abel }
  Â· rw [add_assoc]
  Â· symm
    refine (entropy_add_right hX (by fun_prop) _).trans $
      IndepFun.entropy_pair_eq_add hX (by fun_prop) ?_
    exact h.indepFun_add_right this 0 1 2 (by decide) (by decide)
  Â· rw [eq_comm, â† add_assoc]
    refine (entropy_add_right' hZ (by fun_prop) _).trans $
      IndepFun.entropy_pair_eq_add hZ (by fun_prop) ?_
    exact h.indepFun_add_right this 2 0 1 (by decide) (by decide)

/-- A version of the **Kaimanovich-Vershik inequality** with some variables negated. -/
lemma kaimanovich_vershik' {X Y Z : Î© â†’ G} (h : iIndepFun (fun _ â†¦ hG) ![X, Y, Z] Î¼)
    (hX : Measurable X) (hY : Measurable Y) (hZ : Measurable Z)
    [FiniteRange X] [FiniteRange Z] [FiniteRange Y] :
    H[X - (Y + Z) ; Î¼] - H[X - Y ; Î¼] â‰¤ H[Y + Z ; Î¼] - H[Y ; Î¼] := by
  rw [â† entropy_neg (X := Y + Z) (by fun_prop), â† entropy_neg hY]
  simp_rw [sub_eq_add_neg, neg_add, â† add_assoc]
  refine kaimanovich_vershik ?_ hX ?_ hZ.neg
  convert (h.neg 1).neg 2
  ext i; fin_cases i
  Â· simp (discharger := decide)
  Â· simp (discharger := decide)
  Â· rw [â† show âˆ€ h : 2 < 3, (2 : Fin 3) = âŸ¨2, hâŸ© by intro; rfl]
    simp (discharger := decide)
  Â· exact hY.neg

section BalogSzemerediGowers

/-- The **entropic Balog-SzemerÃ©di-Gowers inequality**. Let $A, B$ be $G$-valued random variables on
$\Omega$, and set $Z := A+B$. Then
$$\sum_{z} P[Z=z] d[(A | Z = z) ; (B | Z = z)] \leq 3 I[A :B] + 2 H[Z] - H[A] - H[B]. $$
TODO: remove the hypothesis of `Fintype G` from here and from `condIndep_copies'` -/
lemma ent_bsg [IsProbabilityMeasure Î¼] {A B : Î© â†’ G} (hA : Measurable A) (hB : Measurable B)
    [Fintype G] :
    (Î¼.map (A + B))[fun z â†¦ d[A ; Î¼[|(A + B) â»Â¹' {z}] # B ; Î¼[|(A + B) â»Â¹' {z}]]]
      â‰¤ 3 * I[A : B; Î¼] + 2 * H[A + B ; Î¼] - H[A ; Î¼] - H[B ; Î¼] := by
  let Z := A + B
  have hZ : Measurable Z := hA.add hB
  obtain âŸ¨Î©', _, ABâ‚, ABâ‚‚, Z', Î½, _, hABâ‚, hABâ‚‚, hZ', hABZ, hABZâ‚, hABZâ‚‚, hZâ‚, hZâ‚‚âŸ© :=
    condIndep_copies' (âŸ¨A, BâŸ©) Z (hA.prod_mk hB) hZ Î¼ (fun (a, b) c â†¦ c = a + b)
    .of_discrete (Eventually.of_forall fun _ â†¦ rfl)
  let Aâ‚ := fun Ï‰ â†¦ (ABâ‚ Ï‰).1
  let Bâ‚ := fun Ï‰ â†¦ (ABâ‚ Ï‰).2
  let Aâ‚‚ := fun Ï‰ â†¦ (ABâ‚‚ Ï‰).1
  let Bâ‚‚ := fun Ï‰ â†¦ (ABâ‚‚ Ï‰).2
  replace hZâ‚ : Z' = Aâ‚ + Bâ‚ := funext hZâ‚
  replace hZâ‚‚ : Z' = Aâ‚‚ + Bâ‚‚ := funext hZâ‚‚
  have hAâ‚ : Measurable Aâ‚ := hABâ‚.fst
  have hBâ‚ : Measurable Bâ‚ := hABâ‚.snd
  have hAâ‚‚ : Measurable Aâ‚‚ := hABâ‚‚.fst
  have hBâ‚‚ : Measurable Bâ‚‚ := hABâ‚‚.snd
  have hZZ' : IdentDistrib Z' Z Î½ Î¼ := hABZâ‚.comp measurable_snd
  have :=
    calc
      H[âŸ¨Aâ‚, âŸ¨Bâ‚, Aâ‚ - Bâ‚‚âŸ©âŸ© ; Î½]
        = H[âŸ¨âŸ¨Aâ‚, Bâ‚âŸ©, âŸ¨âŸ¨Aâ‚‚, Bâ‚‚âŸ©, Z'âŸ©âŸ© ; Î½] := entropy_of_comp_eq_of_comp _
          (hAâ‚.prod_mk $ hBâ‚.prod_mk $ hAâ‚.sub hBâ‚‚) (hABâ‚.prod_mk $ hABâ‚‚.prod_mk hZ')
            (fun (a, b, c) â†¦ ((a, b), (b + c, a - c), a + b))
            (fun ((a, b), (_c, d), _e) â†¦ (a, b, a - d))
          (by funext; simpa [sub_add_eq_add_sub, Prod.ext_iff, â† hZâ‚, hZâ‚‚, two_nsmul, â† add_sub_assoc,
            add_comm, eq_sub_iff_add_eq] using congr_fun (hZâ‚‚.symm.trans hZâ‚) _) rfl
      _ = H[âŸ¨âŸ¨Aâ‚, Bâ‚âŸ©, Z'âŸ© ; Î½] + H[âŸ¨âŸ¨Aâ‚‚, Bâ‚‚âŸ©, Z'âŸ© ; Î½] - H[Z' ; Î½] :=
        ent_of_cond_indep _ hABâ‚ hABâ‚‚ hZ' hABZ
      _ = 2 * H[âŸ¨âŸ¨A, BâŸ©, ZâŸ© ; Î¼] - H[Z ; Î¼] := by
        rw [two_mul]
        congr 1
        congr 1 <;> exact IdentDistrib.entropy_eq â€¹_â€º
        exact hZZ'.entropy_eq
      _ = 2 * H[âŸ¨A, BâŸ© ; Î¼] - H[Z ; Î¼] := by
        congr 2
        exact entropy_prod_comp (hA.prod_mk hB) _ fun x â†¦ x.1 + x.2
  have :=
    calc
      H[âŸ¨Aâ‚, Aâ‚ - Bâ‚‚âŸ© ; Î½]
        = H[âŸ¨Aâ‚, Bâ‚‚âŸ© ; Î½] := entropy_sub_right hAâ‚ hBâ‚‚ _
      _ â‰¤ H[Aâ‚ ; Î½] + H[Bâ‚‚ ; Î½] := entropy_pair_le_add hAâ‚ hBâ‚‚ _
      _ = H[A ; Î¼] + H[B ; Î¼] := by
        congr 1
        exact (hABZâ‚.comp measurable_fst.fst).entropy_eq
        exact (hABZâ‚‚.comp measurable_fst.snd).entropy_eq
  have :=
    calc
      H[âŸ¨Bâ‚, Aâ‚ - Bâ‚‚âŸ© ; Î½]
        = H[âŸ¨Aâ‚‚, Bâ‚âŸ© ; Î½] := by
          rw [entropy_comm hBâ‚ (show Measurable (Aâ‚ - Bâ‚‚) from hAâ‚.sub hBâ‚‚),
            â† entropy_sub_left' hAâ‚‚ hBâ‚, sub_eq_sub_iff_add_eq_add.2 $ hZâ‚.symm.trans hZâ‚‚]
      _ â‰¤ H[Aâ‚‚ ; Î½] + H[Bâ‚ ; Î½] := entropy_pair_le_add hAâ‚‚ hBâ‚ _
      _ = H[A ; Î¼] + H[B ; Î¼] := by
        congr 1
        exact (hABZâ‚‚.comp measurable_fst.fst).entropy_eq
        exact (hABZâ‚.comp measurable_fst.snd).entropy_eq
  have :=
    calc
     _ â‰¤ _ := entropy_triple_add_entropy_le Î½ hAâ‚ hBâ‚ (show Measurable (Aâ‚ - Bâ‚‚) from hAâ‚.sub hBâ‚‚)
     _ â‰¤ _ := add_le_add â€¹_â€º â€¹_â€º
  have :=
    calc
      H[Aâ‚ - Bâ‚‚ | Z' ; Î½]
        â‰¤ H[Aâ‚ - Bâ‚‚ ; Î½] := condEntropy_le_entropy _ (hAâ‚.sub hBâ‚‚) hZ'
      _ â‰¤ _ := le_sub_iff_add_le'.2 â€¹_â€º
      _ = 2 * I[A : B ; Î¼] + H[Z ; Î¼] := by
        rw [â€¹H[âŸ¨Aâ‚, âŸ¨Bâ‚, Aâ‚ - Bâ‚‚âŸ©âŸ© ; Î½] = _â€º, mutualInfo_def]; ring
  have hAâ‚Z :=
    calc
      H[Aâ‚ | Z' ; Î½]
      _ = H[âŸ¨Aâ‚, Bâ‚âŸ© ; Î½] - H[Z' ; Î½] := by
        rw [chain_rule'', hZâ‚, entropy_add_right, entropy_comm] <;> assumption
      _ = H[âŸ¨A, BâŸ© ; Î¼] - H[Z ; Î¼] := by
        congr 1
        exact (hABZâ‚.comp measurable_fst).entropy_eq
        exact hZZ'.entropy_eq
      _ = H[A ; Î¼] + H[B ; Î¼] - I[A : B ; Î¼] - H[Z ; Î¼] := by
        rw [â† entropy_add_entropy_sub_mutualInfo]
  have hBâ‚‚Z :=
    calc
      H[Bâ‚‚ | Z' ; Î½]
      _ = H[âŸ¨Aâ‚‚, Bâ‚‚âŸ© ; Î½] - H[Z' ; Î½] := by
        rw [chain_rule'', hZâ‚‚, entropy_add_right', entropy_comm] <;> assumption
      _ = H[âŸ¨A, BâŸ© ; Î¼] - H[Z ; Î¼] := by
        congr 1
        exact (hABZâ‚‚.comp measurable_fst).entropy_eq
        exact hZZ'.entropy_eq
      _ = H[A ; Î¼] + H[B ; Î¼] - I[A : B ; Î¼] - H[Z ; Î¼] := by
        rw [â† entropy_add_entropy_sub_mutualInfo]
  calc
    (Î¼.map Z)[fun z â†¦ d[A ; Î¼[|Z â† z] # B ; Î¼[|Z â† z]]]
      = (Î½.map Z')[fun z â†¦ d[Aâ‚ ; Î½[|Z' â† z] # Bâ‚‚ ; Î½[|Z' â† z]]] := by
        rw [hZZ'.map_eq]
        refine integral_congr_ae $ Eventually.of_forall fun z â†¦ ?_
        have hAAâ‚ : IdentDistrib Aâ‚ A (Î½[|Z' â† z]) (Î¼[|Z â† z]) :=
          (hABZâ‚.comp $ measurable_fst.fst.prod_mk measurable_snd).cond
            (.singleton z) hZ' hZ
        have hBBâ‚‚ : IdentDistrib Bâ‚‚ B (Î½[|Z' â† z]) (Î¼[|Z â† z]) :=
          (hABZâ‚‚.comp $ measurable_fst.snd.prod_mk measurable_snd).cond
            .of_discrete hZ' hZ
        dsimp (config := {zeta := false}) [rdist]
        rw [â† hAAâ‚.entropy_eq, â† hBBâ‚‚.entropy_eq, hAAâ‚.map_eq, hBBâ‚‚.map_eq]
    _ = (Î½.map Z')[fun z â†¦
          H[Aâ‚ - Bâ‚‚ ; Î½[|Z' â† z]] - H[Aâ‚ ; Î½[|Z' â† z]]/2 - H[Bâ‚‚ ; Î½[|Z' â† z]]/2] := by
        apply integral_congr_ae
        apply hABZ.mono
        intro z hz
        exact (hz.comp measurable_fst measurable_snd).rdist_eq hAâ‚ hBâ‚‚
    _ = H[Aâ‚ - Bâ‚‚ | Z' ; Î½] - H[Aâ‚ | Z' ; Î½] / 2 - H[Bâ‚‚ | Z' ; Î½] / 2 := by
        rw [integral_sub, integral_sub, integral_div, integral_div]
        rfl
        all_goals exact .of_finite
    _ â‰¤ 2 * I[A : B ; Î¼] + H[Z ; Î¼] - H[Aâ‚ | Z' ; Î½] / 2 - H[Bâ‚‚ | Z' ; Î½] / 2 :=
        sub_le_sub_right (sub_le_sub_right â€¹_â€º _) _
    _ = _ := by rw [hAâ‚Z, hBâ‚‚Z]; ring

end BalogSzemerediGowers

variable (Î¼ Î¼') in
/-- Suppose that $(X, Z)$ and $(Y, W)$ are random variables, where $X, Y$ take values in an abelian
group. Then $$d[X | Z ; Y | W] \leq d[X ; Y] + \tfrac{1}{2} I[X : Z] + \tfrac{1}{2} I[Y : W]$$ -/
lemma condRuzsaDist_le [Countable T] {X : Î© â†’ G} {Z : Î© â†’ S} {Y : Î©' â†’ G} {W : Î©' â†’ T}
    [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î¼']
    (hX : Measurable X) (hZ : Measurable Z) (hY : Measurable Y) (hW : Measurable W)
    [FiniteRange X] [FiniteRange Z] [FiniteRange Y] [FiniteRange W] :
      d[X | Z ; Î¼ # Y|W ; Î¼'] â‰¤ d[X ; Î¼ # Y ; Î¼'] + I[X : Z ; Î¼]/2 + I[Y : W ; Î¼']/2 := by
  have hXZ : Measurable (âŸ¨X, ZâŸ© : Î© â†’ G Ã— S):= hX.prod_mk hZ
  have hYW : Measurable (âŸ¨Y, WâŸ© : Î©' â†’ G Ã— T):= hY.prod_mk hW
  obtain âŸ¨Î½, XZ', YW', _, hXZ', hYW', hind, hIdXZ, hIdYW, _, _âŸ© :=
    independent_copies_finiteRange hXZ hYW Î¼ Î¼'
  let X' := Prod.fst âˆ˜ XZ'
  let Z' := Prod.snd âˆ˜ XZ'
  let Y' := Prod.fst âˆ˜ YW'
  let W' := Prod.snd âˆ˜ YW'
  have hX' : Measurable X' := hXZ'.fst
  have hZ' : Measurable Z' := hXZ'.snd
  have hY' : Measurable Y' := hYW'.fst
  have hW' : Measurable W' := hYW'.snd
  have : FiniteRange W' := instFiniteRangeComp ..
  have : FiniteRange X' := instFiniteRangeComp ..
  have : FiniteRange Y' := instFiniteRangeComp ..
  have : FiniteRange Z' := instFiniteRangeComp ..
  have hind' : IndepFun X' Y' Î½ := hind.comp measurable_fst measurable_fst
  rw [show XZ' = âŸ¨X', Z'âŸ© by rfl] at hIdXZ hind
  rw [show YW' = âŸ¨Y', W'âŸ© by rfl] at hIdYW hind
  rw [â† condRuzsaDist_of_copy hX' hZ' hY' hW' hX hZ hY hW hIdXZ hIdYW,
    condRuzsaDist_of_indep hX' hZ' hY' hW' _ hind]
  have hIdX : IdentDistrib X X' Î¼ Î½ := hIdXZ.symm.comp measurable_fst
  have hIdY : IdentDistrib Y Y' Î¼' Î½ := hIdYW.symm.comp measurable_fst
  rw [hIdX.rdist_eq hIdY, hIdXZ.symm.mutualInfo_eq, hIdYW.symm.mutualInfo_eq,
    hind'.rdist_eq hX' hY', mutualInfo_eq_entropy_sub_condEntropy hX' hZ',
    mutualInfo_eq_entropy_sub_condEntropy hY' hW']
  have h := condEntropy_le_entropy Î½ (X := X' - Y') (hX'.sub hY') (hZ'.prod_mk hW')
  linarith [h, entropy_nonneg Z' Î½, entropy_nonneg W' Î½]

variable (Î¼ Î¼') in
lemma condRuzsaDist_le' [Countable T] {X : Î© â†’ G} {Y : Î©' â†’ G} {W : Î©' â†’ T}
    [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î¼']
    (hX : Measurable X) (hY : Measurable Y) (hW : Measurable W)
    [FiniteRange X] [FiniteRange Y] [FiniteRange W] :
    d[X ; Î¼ # Y|W ; Î¼'] â‰¤ d[X ; Î¼ # Y ; Î¼'] + I[Y : W ; Î¼']/2 := by
  rw [â† condRuzsaDist_of_const hX _ _ (0 : Fin 1)]
  refine (condRuzsaDist_le Î¼ Î¼' hX measurable_const hY hW).trans ?_
  simp [mutualInfo_const hX (0 : Fin 1)]

variable (Î¼ Î¼') in
lemma condRuzsaDist_le'_prod [Countable T] {X : Î© â†’ G} {Y : Î©' â†’ G} {W Z : Î©' â†’ T}
    [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î¼']
    (hX : Measurable X) (hY : Measurable Y) (hW : Measurable W) (hZ : Measurable Z)
    [FiniteRange X] [FiniteRange Y] [FiniteRange W] [FiniteRange Z]:
    d[X ; Î¼ # Y|âŸ¨W, ZâŸ© ; Î¼'] â‰¤ d[X ; Î¼ # Y|Z ; Î¼'] + I[Y : W | Z ; Î¼']/2 := by
  rw [condRuzsaDist'_prod_eq_sum _ _ hY hW hZ, condRuzsaDist'_eq_sum hY hZ,
    condMutualInfo_eq_sum hZ, Finset.sum_div, â† Finset.sum_add_distrib]
  gcongr with z
  rw [mul_div_assoc, â† mul_add]
  rcases eq_or_ne (Î¼' (Z â»Â¹' {z})) 0 with hz | hz
  Â· simp [hz]
  Â· have : IsProbabilityMeasure (Î¼'[|Z â»Â¹' {z}]) := cond_isProbabilityMeasure hz
    gcongr
    exact condRuzsaDist_le' _ _ hX hY hW

variable (Î¼) in
lemma comparison_of_ruzsa_distances [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î¼']
    {X : Î© â†’ G} {Y : Î©' â†’ G} {Z : Î©' â†’ G}
    (hX : Measurable X) (hY : Measurable Y) (hZ : Measurable Z) (h : IndepFun Y Z Î¼')
    [FiniteRange X] [FiniteRange Z] [FiniteRange Y] :
    d[X ; Î¼ # Y+ Z ; Î¼'] - d[X ; Î¼ # Y ; Î¼'] â‰¤ (H[Y + Z; Î¼'] - H[Y; Î¼']) / 2 âˆ§
    (Module (ZMod 2) G â†’
      H[Y + Z; Î¼'] - H[Y; Î¼'] = d[Y; Î¼' # Z; Î¼'] + H[Z; Î¼'] / 2 - H[Y; Î¼'] / 2) := by
  obtain âŸ¨Î©'', mÎ©'', Î¼'', X', Y', Z', hÎ¼, hi, hX', hY', hZ', h2X', h2Y', h2Z', _, _, _âŸ© :=
    independent_copies3_nondep_finiteRange hX hY hZ Î¼ Î¼' Î¼'
  have hY'Z' : IndepFun Y' Z' Î¼'' := hi.indepFun (show (1 : Fin 3) â‰  2 by decide)
  have h2 : IdentDistrib (Y' + Z') (Y + Z) Î¼'' Î¼' := h2Y'.add h2Z' hY'Z' h
  have hm : âˆ€ (i : Fin 3), Measurable (![X', Y', Z'] i) :=
    fun i â†¦ by fin_cases i <;> (dsimp; assumption)
  have hXY' : IndepFun X' Y' Î¼'' := hi.indepFun (show (0 : Fin 3) â‰  1 by decide)
  have hYZ' : IndepFun Y' Z' Î¼'' := hi.indepFun (show (1 : Fin 3) â‰  2 by decide)
  have hXYZ' : IndepFun X' (Y' + Z') Î¼'' := by
    symm
    exact hi.indepFun_add_left hm 1 2 0 (by decide) (by decide)
  rw [â† h2X'.rdist_eq h2Y', â† h2X'.rdist_eq h2, â† h2Y'.rdist_eq h2Z',
    â† h2.entropy_eq, â† h2Y'.entropy_eq, â† h2Z'.entropy_eq]
  rw [hXY'.rdist_eq hX' hY', hYZ'.rdist_eq hY' hZ', hXYZ'.rdist_eq hX' (hY'.add hZ')]
  constructor
  Â· linarith [kaimanovich_vershik' hi hX' hY' hZ']
  Â· intro hG
    rw [ZModModule.sub_eq_add Y' Z']
    ring

variable (Î¼) in
/-- Let $X, Y, Z$ be random variables taking values in some abelian group, and with $Y, Z$
independent. Then we have
$$d[X ; Y + Z] -d[X ; Y] \leq \tfrac{1}{2} (H[Y+ Z] - H[Y])$$
$$= \tfrac{1}{2} d[Y ; Z] + \tfrac{1}{4} H[Z] - \tfrac{1}{4} H[Y]$$
and
$$d[X ; Y|Y+ Z] - d[X ; Y] \leq \tfrac{1}{2} \bigl(H[Y+ Z] - H[Z]\bigr)$$
$$= \tfrac{1}{2} d[Y ; Z] + \tfrac{1}{4} H[Y] - \tfrac{1}{4} H[Z]$$
-/
lemma condRuzsaDist_diff_le [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î¼']
    {X : Î© â†’ G} {Y : Î©' â†’ G} {Z : Î©' â†’ G}
    (hX : Measurable X) (hY : Measurable Y) (hZ : Measurable Z) (h : IndepFun Y Z Î¼')
    [FiniteRange X] [FiniteRange Z] [FiniteRange Y] :
    d[X ; Î¼ # Y+ Z ; Î¼'] - d[X ; Î¼ # Y ; Î¼'] â‰¤ (H[Y + Z; Î¼'] - H[Y; Î¼']) / 2 :=
  (comparison_of_ruzsa_distances Î¼ hX hY hZ h).1

variable (Î¼) [Module (ZMod 2) G] in
lemma entropy_sub_entropy_eq_condRuzsaDist_add [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î¼']
    {X : Î© â†’ G} {Y : Î©' â†’ G} {Z : Î©' â†’ G}
    (hX : Measurable X) (hY : Measurable Y) (hZ : Measurable Z) (h : IndepFun Y Z Î¼')
    [FiniteRange X] [FiniteRange Z] [FiniteRange Y] :
    H[Y + Z; Î¼'] - H[Y; Î¼'] = d[Y; Î¼' # Z; Î¼'] + H[Z; Î¼'] / 2 - H[Y; Î¼'] / 2 :=
  (comparison_of_ruzsa_distances Î¼ hX hY hZ h).2 â€¹_â€º

variable (Î¼) [Module (ZMod 2) G] in
lemma condRuzsaDist_diff_le' [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î¼']
    {X : Î© â†’ G} {Y : Î©' â†’ G} {Z : Î©' â†’ G}
    (hX : Measurable X) (hY : Measurable Y) (hZ : Measurable Z) (h : IndepFun Y Z Î¼')
    [FiniteRange X] [FiniteRange Z] [FiniteRange Y] :
    d[X ; Î¼ # Y + Z; Î¼'] - d[X ; Î¼ # Y; Î¼'] â‰¤
    d[Y; Î¼' # Z; Î¼'] / 2 + H[Z; Î¼'] / 4 - H[Y; Î¼'] / 4 := by
  linarith [condRuzsaDist_diff_le Î¼ hX hY hZ h, entropy_sub_entropy_eq_condRuzsaDist_add Î¼ hX hY hZ h]

variable (Î¼) in
lemma condRuzsaDist_diff_le'' [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î¼']
    {X : Î© â†’ G} {Y : Î©' â†’ G} {Z : Î©' â†’ G}
    (hX : Measurable X) (hY : Measurable Y) (hZ : Measurable Z) (h : IndepFun Y Z Î¼')
    [FiniteRange X] [FiniteRange Z] [FiniteRange Y] :
    d[X ; Î¼ # Y|Y+ Z ; Î¼'] - d[X ; Î¼ # Y ; Î¼'] â‰¤ (H[Y+ Z ; Î¼'] - H[Z ; Î¼'])/2 := by
  rw [â† mutualInfo_add_right hY hZ h]
  linarith [condRuzsaDist_le' (W := Y + Z) Î¼ Î¼' hX hY (by fun_prop)]

variable (Î¼) [Module (ZMod 2) G] in
lemma condRuzsaDist_diff_le''' [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î¼']
    {X : Î© â†’ G} {Y : Î©' â†’ G} {Z : Î©' â†’ G}
    (hX : Measurable X) (hY : Measurable Y) (hZ : Measurable Z) (h : IndepFun Y Z Î¼')
    [FiniteRange X] [FiniteRange Z] [FiniteRange Y] :
    d[X ; Î¼ # Y|Y+ Z ; Î¼'] - d[X ; Î¼ # Y ; Î¼'] â‰¤
    d[Y ; Î¼' # Z ; Î¼']/2 + H[Y ; Î¼']/4 - H[Z ; Î¼']/4 := by
  linarith [condRuzsaDist_diff_le'' Î¼ hX hY hZ h, entropy_sub_entropy_eq_condRuzsaDist_add Î¼ hX hY hZ h]

variable (Î¼) in
lemma condRuzsaDist_diff_ofsum_le [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î¼']
    {X : Î© â†’ G} {Y Z Z' : Î©' â†’ G}
    (hX : Measurable X) (hY : Measurable Y) (hZ : Measurable Z) (hZ' : Measurable Z')
    (h : iIndepFun (fun _ â†¦ hG) ![Y, Z, Z'] Î¼')
    [FiniteRange X] [FiniteRange Z] [FiniteRange Y] [FiniteRange Z'] :
    d[X ; Î¼ # Y + Z | Y + Z + Z'; Î¼'] - d[X ; Î¼ # Y; Î¼'] â‰¤
    (H[Y + Z + Z'; Î¼'] + H[Y + Z; Î¼'] - H[Y ; Î¼'] - H[Z' ; Î¼'])/2 := by
  have hadd : IndepFun (Y + Z) Z' Î¼' :=
    (h.indepFun_add_left (Fin.cases hY <| Fin.cases hZ <| Fin.cases hZ' Fin.rec0) 0 1 2
      (show 0 â‰  2 by decide) (show 1 â‰  2 by decide))
  have h1 := condRuzsaDist_diff_le'' Î¼ hX (show Measurable (Y + Z) by fun_prop) hZ' hadd
  have h2 := condRuzsaDist_diff_le Î¼ hX hY hZ (h.indepFun (show 0 â‰  1 by decide))
  linarith [h1, h2]
